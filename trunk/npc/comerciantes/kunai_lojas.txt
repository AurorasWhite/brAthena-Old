//  ________________________________________________________
// /                                                        \
// |       _           _   _   _                            |
// |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
// |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
// |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
// |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
// |                                                        |
// |                    brAthena Script                     |
// |--------------------------------------------------------|
// | Nome do Script: Kunai Merchant Kashin					|
// |--------------------------------------------------------|
// | Criado por: eA/brA Dev. Team                           |
// |--------------------------------------------------------|
// | Versões eA/brA: 1.3/1.0                                |
// |--------------------------------------------------------|
// | Descrição:	Trades a few shurikens + ninja stones 		|
// | for elemental kunai.									|
// |--------------------------------------------------------|
// | Changelog:               	                            |
// | 1.0 Script Traduzido [insidemybrain]		            |
// | 1.1 Script Revisado [insidemybrain]		            |
// | - Anotações		   			    					| 
// \________________________________________________________/

que_ng,72,29,3	script	Mercador de Kunais	83,{
	if(BaseJob == Job_Ninja) {
		mes "[Kashin]";
		mes "Sou Kashin, distribuidor";
		mes "de Kunais para Ninjas. Dê";
		mes "uma olhada e me diga se";
		mes "você está interessado em";
		mes "alguns dos itens que tenho.";
		next;
		switch(select("10 Kunais - Veneno Mortal:10 Kunais - Nevasca:10 Kunais - Sopro do Vento:10 Kunais - Terra Negra:10 Kunais - Onda de Calor:Cancelar")) {
		case 1: callfunc "Kunai_Trade",13250,20,7524,1,13259; break;
		case 2: callfunc "Kunai_Trade",13251,8,7522,2,13255; break;
		case 3: callfunc "Kunai_Trade",13252,4,7523,2,13257; break;
		case 4: callfunc "Kunai_Trade",13253,2,7524,1,13256; break;
		case 5: callfunc "Kunai_Trade",13254,1,7521,2,13258; break;
		case 6:
			mes "[Kashin]";
			mes "Bem, obrigado então";
			mes "por visitar a minha loja.";
			mes "Sinta-se a vontade";
			mes "para voltar caso queira";
			mes "algumas Kunais. Adeus por enquanto~";
			close;
		}
	}
	mes "[Kashin]";
	mes "Sou Kashin, distribuidor";
	mes "de Kunais para Ninjas. Se você";
	mes "tem alguns amigos que sejam";
	mes "Ninjas, conte para eles";
	mes "sobre mim.";
	close;
}

function	script	Kunai_Trade	{
	mes "[Kashin]";
	mes "Você pode trocar:";
	mes ""+getarg(1)+" "+getitemname(getarg(0))+" e";
	mes ""+getarg(3)+" "+getitemname(getarg(2))+" por";
	mes "um pacote de 10 "+getitemname(getarg(4))+".";
	next;
	mes "[Kashin]";
	mes "Eu só posso lhe dar 500 pacotes de Kunai por vez.";
	mes "Caso você queira cancelar a negociação digite o valor ''0''. Quantos";
	mes "pacotes de Kunai você gostaria?";
	next;
	input .@amount;
	if(.@amount < 1 || .@amount > 500) {
		mes "[Kashin]";
		mes "Hã? Me desculpe";
		mes "eu não posso lhe dar esse";
		mes "número de pacotes de Kunais. Por favor,";
		mes "escreva um valor menor que 500.";
		close;
	}
	if(countitem(getarg(0)) < .@amount*getarg(1) || countitem(getarg(2)) < .@amount*getarg(3)) {
		mes "[Kashin]";
		mes "Hmm, você não tem";
		mes "a quantidade de item certa";
		mes "para esse pacote de Kunai. Por favor";
		mes "reveja seus itens e volte.";
		close;
 	}
	if(checkweight(getarg(4), .@amount*10) == 0) {
		mes "[Kashin]";
		mes "Hmm, parece que você";
		mes "não tem espaço no seu inventório.";
		mes "Arrume algum espaço nele";
		mes "e volte para que podemos";
		mes "finalizar a nossa negociação.";
		close;
	}
	mes "[Kashin]";
	mes "Ótimo, está tudo em";
	mes "ordem, agora só é preciso";
	mes "completar a negociação.";
	mes "Tenho certeza que você";
	mes "ficará muito satisfeito";
	mes "com essas Kunais.";
	delitem getarg(0),getarg(1)*.@amount;
	delitem getarg(2),getarg(3)*.@amount;
	getitem getarg(4),10*.@amount;
	close;
}
