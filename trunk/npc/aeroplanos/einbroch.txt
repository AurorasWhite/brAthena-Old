//  ________________________________________________________
// /                                                        \
// |       _           _   _   _                            |
// |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
// |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
// |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
// |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
// |                                                        |
// |                     brAthena Script                    |
// |--------------------------------------------------------|
// | Nome do Script: Equipe do Aeroporto de Einbroch	    |
// |--------------------------------------------------------|
// | Criado por: eA/brA Dev. Team                           |
// |--------------------------------------------------------|
// | Versões eA/brA: 1.4/1.1                                |
// |--------------------------------------------------------|
// | Descrição: NPC's do Aeroplano de Einbroch.             |
// |--------------------------------------------------------|
// | Changelog:               	                            |
// | 1.0 Script Traduzido [Tyler]                           |
// | 1.1 Script Revisado [Dani]                             |
// \________________________________________________________/

airport,126,43,4	script	Equipe do Aeroporto#01::AirportE	90,{
	mes "[Funcionária do Aeroporto]";
	mes "Seja bem-vindo ao aeroporto.";
	mes "Como eu posso te ajudar?";
	next;
	if (select("Embarcar no Aeroplano.:Cancelar.") == 1) {
		mes "[Funcionária do Aeroporto]";
		mes "O embarque no Aeroplano está";
		mes "custando 1.200 zenys, mas se você";
		mes "possuir um Bilhete para Aeroplano,"; 
		mes "este pagamento será desconsiderado.";
		mes "Você deseja embarcar no Aeroplano?";
		next;
		if (select("Sim.:Não.") == 1) {
			if(countitem(7311) > 0) {
				delitem 7311,1;
				warp "airport",148,51;
				close;
			}
			if(Zeny >= 1200) {
				set Zeny, Zeny - 1200;
				warp "airport",148,51;
				close;
			}
			mes "[Funcionária do Aeroporto]";
			mes "Você não possui zenys suficientes.";
			close;
		}
	}
	mes "[Funcionária do Aeroporto]";
	mes "Muito obrigada";
	mes "e tenha um ótimo dia!";
	close;
}

airport,143,43,4	duplicate(AirportE)	Equipe do Aeroporto#02	90
airport,156,43,4	duplicate(AirportE)	Equipe do Aeroporto#03	90

airport,126,51,4	script	Equipe do Aeroporto#04::AirportE2	90,{
	mes "[Funcionária do Aeroporto]";
	mes "Bem-vindo!";
	mes "Por favor, siga em frente";
	mes "para embarcar no Aeroplano.";
	next;
	if (select("Deixar o Terminal Principal.:Cancelar.") == 1) {
		mes "[Funcionária do Aeroporto]";
		mes "Se você sair do Terminal";
		mes "Principal agora, você terá";
		mes "de pagar novamente a taxa de";
		mes "embarque para poder entrar";
		mes "novamente no Aeroplano. Você";
		mes "tem certeza que deseja sair do Terminal?";
		next;
		if (select("Sim.:Não.") == 1) {
			warp "airport",142,40;
			close;
		}
	}
	mes "[Funcionária do Aeroporto]";
	mes "Certo, obrigado por contribuir";
	mes "com o Aeroporto com seu pagamento.";
	mes "Eu espero que você faça";
	mes "uma viagem muito agradável!";
	close;
}

airport,143,51,4	duplicate(AirportE2)	Equipe do Aeroporto#05	90
airport,156,51,4	duplicate(AirportE2)	Equipe do Aeroporto#06	90

einbroch,94,267,4	script	Equipe do Aeroporto#07	91,{
	mes "[Funcionária do Aeroporto]";
	mes "Seja bem-vindo ao";
	mes "Aeroporto de Einbroch.";
	mes "Por favor, use esta porta para";
	mes "embarcar no Aeroplano. Ele fará";
	mes "paradas em Lighthalzen e em Juno";
	mes "na República de Schwartzwald.";
	next;
	mes "[Funcionária do Aeroporto]";
	mes "Caso contrário, se Einbroch for";
	mes "seu destino desejado,";
	mes "siga e desça as escadas";
	mes "e fale com a Funcionária de Desembarque para";
	mes "seguir para o Terminal Principal. Agradecemos";
	mes "desde já, aprecie e curta as suas viagens!";
	close;
}
