//  ________________________________________________________
// /                                                        \
// |       _           _   _   _                            |
// |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
// |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
// |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
// |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
// |                                                        |
// |                     brAthena Script                    |
// |--------------------------------------------------------|
// | Nome do Script: Equipe do Aeroporto de Yuno            |
// |--------------------------------------------------------|
// | Criado por: eA/brA Dev. Team                           |
// |--------------------------------------------------------|
// | Versões eA/brA: 1.3/1.0                                |
// |--------------------------------------------------------|
// | Descrição: -                                           |
// |--------------------------------------------------------|
// | Changelog:               	                            |
// | 1.0 Modificação [Autor]                                |
// | - Anotações		   			   						| 
// \________________________________________________________/

y_airport,126,43,4	script	Airport Staff#001::AirportY	90,{
	mes "[Airport Staff]";
	mes "Welcome to the Airport.";
	mes "How may I help you?";
	next;
	if (select("Board the Airship:Cancel") == 1) {
		mes "[Airport Staff]";
		mes "The Airship boarding fee";
		mes "is 1,200 zeny, but if you've";
		mes "got a Free Ticket for Airship,"; 
		mes "the fee will be waived. Will";
		mes "you board the Airship?";
		next;
		if (select("Yes:No") == 1) {
			if(countitem(7311) > 0) {
				delitem 7311,1;
				warp "y_airport",148,51;
				close;
			}
			if(zeny >= 1200) {
				set Zeny,zeny-1200;
				warp "y_airport",148,51;
				close;
			}
			mes "[Airport Staff]";
			mes "You don't have enough zeny.";
			close;
		}
	}
	mes "[Airport Staff]";
	mes "Thank you and";
	mes "have a nice day.";
	close;
}

y_airport,143,43,4	duplicate(AirportY)	Airport Staff#002	90
y_airport,156,43,4	duplicate(AirportY)	Airport Staff#003	90

y_airport,126,51,4	script	Airport Staff#004::AirportY2	90,{
	mes "[Airport Staff]";
	mes "Welcome~";
	mes "Please head this";
	mes "way to board the Airship.";
	next;
	if (select("Exit to Main Terminal:Cancel") == 1) {
		mes "[Airport Staff]";
		mes "If you leave the";
		mes "main terminal, you'll";
		mes "have to pay the admission";
		mes "fee again in order to board";
		mes "the Airship. Are you sure";
		mes "that you want to exit?";
		next;
		if (select("Yes:No") == 1) {
			warp "y_airport",142,40;
			close;
		}
	}
	mes "[Airport Staff]";
	mes "Alright, thank you";
	mes "for your patronage";
	mes "and I hope you have";
	mes "a pleasant flight~";
	close;
}

y_airport,143,51,4	duplicate(AirportY2)	Airport Staff#005	90
y_airport,156,51,4	duplicate(AirportY2)	Airport Staff#006	90
