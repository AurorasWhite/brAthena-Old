//  ____________________________________________________________
// /                                                            \
// |       _           _   _   _                                |
// |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _           |
// |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |          |
// |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |          |
// |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|          |
// |                                                            |
// |                    brAthena Script                         |
// |------------------------------------------------------------|
// | Nome do Script: Evento de Resetar as Habilidades Oficial   |
// | em Yuno.                                                   |
// |------------------------------------------------------------|
// | Criado por: eA/brA Dev. Team                               |
// |------------------------------------------------------------|
// | Versões eA/brA: 1.02/1.1                                   |
// |------------------------------------------------------------|
// | Descrição:                                                 |
// | Reset de habilidades com pagamento, Nv. Base mínimo: 60    |
// | A cada Nível de Base precisa de 20.000 Zenys, seja         |
// | cuidadoso para usar habilidades.                           |
// |------------------------------------------------------------|
// | Changelog:                                                 |
// | 1.1 Modificação [Heaven]                                   |
// | - Traduzido                                                | 
// \____________________________________________________________/

yuno,138,187,4  script  Professor Hipnotizador  124,{
        set @npcname$,"[^D5A500Hipnotizador^000000]";
        mes @npcname$;

        if(MISC_QUEST & 1024){
                mes "Eu já lhe disse que você só pode completar este evento uma vez.";
                close;
        }
        if(BaseLevel < 60) {
                mes "Por favor, retorne quando você chegar ao Nv. de Base 60 ou maior.";
                close;
        }
        if(SkillPoint != 0) {
                mes "Você vai precisar de usar todos os seus pontos de habilidade se você que eu continue.";
                mes "Por favor, volte em breve!";
                close;
        }
        if(Weight || checkfalcon() || checkcart() || checkriding()) {
                mes "Você gostaria de resetar suas habilidades?";
                mes "Desculpe-me, mas...";
                next;
                mes @npcname$;
                if(Weight) {
                        mes "Não se pode resetar suas habilidades";
                        mes "enquanto se está carrega";
                        mes "algum item.";
                } else if(checkcart())
                        mes "Por favor, deixe seu carrinho e vamos continuar.";
                else if(checkfalcon())
                        mes "Por favor, liberte seu Falcão e vamos continuar.";
                else if(checkriding())
                        mes "Por favor, liberte seu Peco Peco e vamos continuar.";
                next;
                mes @npcname$;
                mes "Volte em breve!";
                close;
        }
        mes "Oi, eu sou o Resetador de Habilidades.";
        mes "Seu nome é ^0080FF"+strcharinfo(0)+"^000000.";
        mes "Como posso ajudá-lo?";
        next;
        menu "^009500Informações sobre Resetar habilidades.^000000",L_Info,"^00B6FFResetar habilidades.^000000",L_Reset,"^000088Não se preocupe^000000",-;

        mes @npcname$;
        mes "Você sabe onde me encontrar,";
        mes "se você quiser resetar!!"; 
        close;

L_Info: 
        mes @npcname$;
        mes "Resetar as habilidades não é GRATUITO!!";
        mes "O gasto para resetar as habilidades é de^D5A50020000 Zeny x Nível de Base^000000.";
        mes "Pois é ...cada Nível de Base custa 20000 Zenys para resetar as habilidades.";
        next;
        mes @npcname$;
        mes "Ah sim, mais uma coisa!";
        mes "Qualquer carta, falcão ou Peco Peco que você estiver equipado";
        mes "será removido se você resetar as habilidades.";
        next;
        mes @npcname$;
        mes "Apenas mais uma vez para resetar os pontos de habilidade";
        mes "Cuidado com suas habilidades daqui em diante.";
        close;
        
L_Reset:
        mes @npcname$;
        mes "Antes de resetar as habilidades.";
        mes "Você deve primeiramente me dizer seu Nível de Base."; 
        next;
        mes "^D5A500[" + strcharinfo(0) + "]^000000";
        mes "Meu Nível de Base é ^AA00AANível de Base " + BaseLevel + ".^000000"; 
        next;
        set @zeny,BaseLevel*20000;
        mes @npcname$;
        mes "O total de zenys para resetar as habilidades é de ^529DFF" + @zeny + "Zenys^000000.";
        next;
        if(Zeny < @zeny) {
                mes @npcname$;
                mes "Parece que você não tem dinheiro suficiente.";
                mes "Além disso, nós aguardamos a oportunidade.";
                emotion e_hmm;
                close;
        }
        set Zeny, Zeny-@zeny;
        set MISC_QUEST,MISC_QUEST | 1024;
        resetskill;
        mes @npcname$;
        mes "Obrigado.";
        emotion e_thx;
        logmes "SKILL RESET EVENT";
        close;
}