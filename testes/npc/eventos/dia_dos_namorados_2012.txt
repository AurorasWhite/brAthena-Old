//    ________________________________________________________ 
//   /                                                        \
//   |       _           _   _   _                            |
//   |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
//   |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
//   |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
//   |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
//   |                                                        |
//   |                    brAthena Script                     |
//   |--------------------------------------------------------|
//   | Nome do Script: dia_dos_namorados_2012.txt             |
//   |--------------------------------------------------------|
//   | Criado por: eA/rA/brA Dev. Team                        |
//   |--------------------------------------------------------|
//   | Versões eA/rA/brA: 1.1/1.0                             |
//   |--------------------------------------------------------|
//   | Descrição: Evento do Dia dos Namorados. (2012)         |
//   |--------------------------------------------------------|
//   | Changelog:                                             |
//   | 1.0 Criado Cabeçalho [Megasantos]                      |
//   |--------------------------------------------------------|
//   | - Anotações                                            |
//   \________________________________________________________/

mosk_in,21,246,5	script	Pinkamenia	71,{
	set .@n$,"^0000FF[Pinkamenia]^000000";
	if (#V_QUE12==3) {
		set .@FlorAID,7864;
		setarray .@DocesID[0],538,558,539,573,559,560,12062,596,597,12414,12319;
		setarray .@DocesCusto[0],1,2,5,10,10,10,15,15,15,20,20;
		mes .@n$;
		mes "Olá "+strcharinfo(0)+", você";
		mes "deseja negociar alguns itens?";
		mes "Você terá que me dar Flores";
		mes "do Amor, é claro!";
		next;
		if(select("Sim!","Não, nem pensar.") == 2) close;
		next;
		mes .@n$;
		mes "Selecione um item.";
		mes "A quantidade de Flores do Amor de que você precisa está entre parênteses.";
		for(set .@i,0; .@i < getarraysize(.@DocesID); set .@i,.@i+1)
			set .@menu$, .@menu$+"^00AA00["+.@DocesCusto[.@i]+"]^000000 "+getitemname(.@DocesID[.@i])+":";
		next;
		set .@choice, select(.@menu$)-1;
		mes .@n$;
		if (countitem(.@FlorAID) < .@DocesCusto[.@choice]) {
			mes "Você vai precisar de mais flores amor para conseguir esse item!";
			close;
		}
		mes "Tem certeza de que quer negociar ^00aa00"+.@DocesCusto[.@choice]+"x Flor do Amor ^000000 por ^0055FF"+getitemname(.@DocesID[.@choice])+"^000000?";
		if(select("Não, eu mudei de idéia.:Sim, trocar!") == 1) close;
		set .@new, .@FlorAID - .@DocesCusto[.@choice];
		if (.@new < 0) set .@new, 0;
		delitem .@FlorAID, .@DocesCusto[.@choice];
		getitem .@DocesID[.@choice],1;
		mes "Divirta-se com o seu item!";
		close;
	} else if (#V_QUE12==2) {
		mes .@n$;
		if (countitem(7864) < 15) {
			mes "Você precisa me trazer 15 Flores do Amor!";
			close;
		}
		mes "Muito obrigado por";
		mes "trazer minhas flores de volta!";
		delitem 7864,15;
		next;
		mes .@n$;
		mes "Receba este pequeno";
		mes "'agradecimento'.";
		getexp 500000,400000;
		getitem 617,1;
		getitem 12319,2;
		next;
		mes .@n$;
		mes "Se você possuir mais Flores do";
		mes "Amor, você pode trocá-las";
		mes "por mais alguns doces.";
		mes "Até breve!";
		set #V_QUE12,3;
		close;
	} else if (#V_QUE12==1) {
		mes .@n$;
		mes "Por favor me traga um Chapéu de Bolo +8";
		mes "to the Baker Extraordinaire";
		mes "standing next to me!";
		close;
	} else if (BaseLevel >= 45) {
		mes .@n$;
		mes "Olá "+strcharinfo(0)+", é o";
		mes "Dia dos Namorados e nós";
		mes "amamos entregar chocolates";
		mes "doces.";
		next;
		mes .@n$;
		mes "Nosso problema é que nós";
		mes "precisamos de uma pessoa forte";
		mes "como você, que poderia nos";
		mes "ajudar, mas primeiro você deve";
		mes "entregar o Chapéu de Bolo +8";
		mes "to the Baker Extraordinaire, que";
		mes "está de pé ao meu lado!";
		set #V_QUE12,1;
		close;
	}
	mes .@n$;
	mes "Olá "+strcharinfo(0)+"!";
	close;

OnInit:
	// Taxa de Drop - Item Desconhecido (atualmente a taxa é 5%)
	setitemscript 5105,"{ bonus bDex,1; bonus bMaxSP,80; bonus3 bAddMonsterDropItem,7864,RC_DemiHuman,500; }",0;
	end;
}

mosk_in,21,244,3	script	Baker Extraordinaire	410,{
	set .@n$,"^0000FF[Baker Extraordinaire]^000000";
	if(#V_QUE12==3) {
		mes .@n$;
		mes "Troque suas Flores do Amor com Pinkamenia!";
		close;
	} else if(#V_QUE12==2) {
		mes .@n$;
		mes "Entregue 15 Flores do Amor para Pinkamenia!";
		close;
	} else if(#V_QUE12==1) { 
		mes .@n$;
		if (getequipid(1) == 5024 && getequiprefinerycnt(1) >= 8) {
			mes "Ah, Pinkamenia disse para";
			mes "você me entregar um +8 Chapéu de";
			mes "Bolo. Agora eu vou";
			mes "trocar o seu +8 Chapéu de";
			mes "Bolo por outro Chapéu de Bolo.";
			next;
			mes .@n$;
			delitem 5024,1;
			getitem 5105,1;
			equip 5105;
			mes "Se você usar o";
			mes "novo Chapéu de Bolo, existe";
			mes "uma oportunidade de";
			mes "Monstros Humanóides";
			mes "derrubarem a Flor do Amor!";
			next;
			mes .@n$;
			mes "Os monstros roubaram nossas Flores,";
			mes "que são precisas para";
			mes "criar os chocolates. Agora junte";
			mes "15 Flores do Amor e entregue";
			mes "para Pinkamenia!";
			set #V_QUE12,2;
			close;
		} else {
			mes "Desculpe, mas onde está o seu";
			mes "+8 Chapéu de Bolo? Traga-o";
			mes "para mim, e lembre-se";
			mes "de equipá-lo!";
			close;
		}
	} else {
		mes .@n$;
		mes "Olá, "+strcharinfo(0)+"!";
		close;
	}
}