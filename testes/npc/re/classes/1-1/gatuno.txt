//    ________________________________________________________ 
//   /                                                        \
//   |       _           _   _   _                            |
//   |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
//   |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
//   |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
//   |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
//   |                                                        |
//   |                    brAthena Script                     |
//   |--------------------------------------------------------|
//   | Nome do Script: gatuno.txt                             |
//   |--------------------------------------------------------|
//   | Criado por: eA/rA/brA Dev. Team                        |
//   |--------------------------------------------------------|
//   | Versões eA/rA/brA: 1.8/1.0/1.1                         |
//   |--------------------------------------------------------|
//   | Descrição: Mudança de Classe para Gatuno.              |
//   |--------------------------------------------------------|
//   | Changelog:                                             |
//   | 1.0 Criado Cabeçalho [Megasantos]                      |
//   | 1.1 Tradução PT-BR [Holy]                              |
//   |--------------------------------------------------------|
//   | - Anotações                                            |
//   \________________________________________________________/

moc_prydb1,39,129,2	script	Gatuna Ajudante#gatuno	69,{
	if (Upper == 1) {
		if (ADVJOB == Job_Assassin_Cross || ADVJOB == Job_Stalker) {
			if (Class == Job_Novice_High) {
				mes "[Gatuna Ajudante]";
				mes "Hã? Eu te conheço? É assustador que você parece tão familiar. Você não tem um irmão gêmeo, que pareça com você?";
				next;
				if (getskilllv("NV_BASIC") < 9) {
					mes "[Gatuna Ajudante]";
					mes "O que, você quer ser um gatuno? Sinto muito, mas parece que você precisa de mais treinamento.";
					next;
					mes "[Gatuna Ajudante]";
					mes "Gaste seu tempo e aprenda todas as habilidades básicas, certo? Bem, então vejo você mais tarde~!";
					close;
				}
				mes "[Gatuna Ajudante]";
				mes "Eu tenho essa sensação de que você já passou por uma vida inteira de lutas, então eu estou promovendo-o a um Gatuno neste minuto. É melhor eu deixar caras Durões cuidando de você...";
				next;
				skill 143,0,0;
				jobchange Job_Thief_High;
				skill 149,1,0;
				skill 150,1,0;
				skill 151,1,0;
				skill 152,1,0;
				mes "[Gatuna Ajudante]";
				mes "Ao se tornar um gatuno, irá viver como um Gatuno. Agora, vá em frente! Próximo~";
				close;
			}else{
				mes "[Gatuna Ajudante]";
				if (sex)
					mes "Ei, cara.";
				else
					mes "Ei, moça~";
				close;
			}
		}else{
			mes "[Gatuna Ajudante]";
			if (sex)
				mes "Ei, cara.";
			else
				mes "Ei, moça.";
			mes "...Ei! Você parece muito uma boa pessoa para querer ser um gatuno! Agora suma, estou ocupado. Próximo!";
			close;
		}
	}
	if(q_job_thief == 1) {
		mes "[Gatuna Ajudante]";
		mes "É... Parece que você é "+(Sex?"filho":"filha")+" de uma família rica, mas por que você está aqui neste lugar sujo?";
		next;
		mes "[Gatuna Ajudante]";
		mes "Bem, eu não sou o encarregado de te tornar um gatuno. Eu só aceito pedidos, entendeu?";
		mes "Se você quer se tornar um gatuno, fale com esse cara ao meu lado.";
		close;
	}
	if(Class != Job_Novice) {
		if(Class == Job_Thief) {
			mes "[Gatuna Ajudante]";
			mes "Ei~ Se você tiver algum problema, fale quando quiser, tudo bem?";
			close;
		}else{
			if(Sex) {
				mes "[Gatuna Ajudante]";
				mes "O que é...?";
				mes "Hum.. você é um "+jobname(Class)+" tolo...!";
				next;
				mes "[Gatuna Ajudante]";
				mes "Ei, irmão.";
				mes "Por que você está aqui? Volte para o seu lugar ~~";
				close;
			}else{
				mes "[Gatuna Ajudante]";
				mes "O que é...?";
				next;
				mes "[Gatuno da Guilda]";
				mes "Por que você está aqui? Volte para o seu lugar ~~";
				close;
			}
		}
	}
	mes "[Gatuna Ajudante]";
	mes "Ehh... Parece que você é "+(Sex?"filho":"filha")+" de uma família rica, mas por que você está aqui neste lugar sujo?";
	next;
	if(select("Eu quero me tornar um Gatuno.:Nada.") == 2) {
		mes "[Gatuno da Guilda]";
		mes "Você sabe que não pode ser um gatuno sem uma aplicação.....";
		mes "O que está pensando..?";
		close;
	}
	mes "[Gatuna Ajudante]";
	mes "Bem, você é o orgulho deles?";
	mes "Você está me dizendo que quer se tornar um gatuno! Por que você não vai a todas as aldeias e anuncia que é um  gatuno?";
	mes "'Hahaha! Vá colocar uma bandeira que diz: Eu vou ser um ladrão que rouba orgulhoso coisas de outras pessoas.'";
	next;
	mes "[Gatuna Ajudante]";
	mes "Você quer ser um gatuno do mal?";
	next;
	switch(select("Sim.:Não.:O que acha?")) {
	case 1:
		mes "[Gatuna Ajudante]";
		mes "Ah, você...? Hum.. bem... Eu vivo e aprendo a ver pessoas estranhas como você.";
		next;
		break;
	case 2:
		mes "[Gatuna Ajudante]";
		mes "Então, por que você está aqui? Você acha que pode se tornar um gatuno tão facilmente?";
		next;
		break;
	case 3:
		mes "[Gatuna Ajudante]";
		mes "Hã..? Eu? Eu?";
		mes "Bem... Eu só tenho vocação para ser um gatuno... caracteristicamente... Eu não me importo com assuntos bobos.";
		next;
		break;
	}
	mes "[Gatuna Ajudante]";
	mes "Enfim, no mundo exterior nunca diga que você quer ser um gatuno!!";
	next;
	mes "[Gatuna Ajudante]";
	mes "Então, você quer se candidatar a ser um Gatuno?";
	next;
	if(select("Sim, eu quero.:Não.") == 2) {
		mes "[Gatuna Ajudante]";
		mes "Bem... faça o que quiser, siga seu caminho~";
		close;
	}
	mes "[Gatuna Ajudante]";
	mes "Certo, me diga seu nome. Hã? O que?";
	mes ""+strcharinfo(0)+". Então é "+strcharinfo(0)+", certo...?";
	next;
	if (getskilllv("NV_BASIC") < 9) {
		mes "[Gatuna Ajudante]";
		mes "Eu posso ver a sua forte vontade de se tornar um Gatuno......";
		mes "Mas só com a sua vontade, você não pode vencer em uma luta real, não é?";
		mes "Então, vá e atinja pelo menos o Nível de Habilidades Básicas 9.";
		close;
	}
	mes "[Gatuna Ajudante]";
	mes "Hum.... Eu encontrei seus interessantes registros criminais a partir dos dados da agência de detetives. Bem parece que você tem o que é preciso para ser um bom gatuno.";
	mes ""+strcharinfo(0)+". Eu agora declaro que você passou na entrevista de classe!";
	next;
	mes "[Gatuna Ajudante]";
	mes "Bem, Eu não sou o responsável por torná-lo um Gatuno. Eu só aceito pedidos, entendeu?";
	mes "Se você quer se tornar um gatuno, fale com o cara próximo a mim.";
	set q_job_thief,1;
	close;
}

moc_prydb1,42,133,2	script	Brad#gatuno	118,{
	if(q_job_thief == 1) {
		mes "[Brad]";
		mes "Certo. Você conseguiu passar na entrevista, correto?";
		mes "Bom. Eu irei aceitá-lo.";
		mes "Vamos começar a cerimônia de mudança de classe da nossa Guilda!";
		next;
		mes "[Brad]";
		mes "'"+strcharinfo(0)+".'";
		mes "'Para aqueles que estão prontos, o luar deve abrir o caminho. Agora você está totalmente pronto, oficialmente permitem que você se torne um membro da Guilda dos Gatunos.'";
		jobchange Job_Thief;
		set q_job_thief,0;
		getitem 13041,1; // Main Gauche de Iniciante
		next;
		mes "[Brad]";
		mes "'Parabéns por se tornar um Gatuno.'";
		mes "'A partir de agora, manter as regras da nossa aliança e ser um membro honorário.'";
		mes "'Se você nos trazer desgraça por violarem as nossas regras, é melhor você assistir a sua volta.'";
		mes "'De qualquer forma, eu espero que você seja um grande Gatuno.'";
		next;
		mes "[Brad]";
		mes "Heee~Yaaaa~! Parabéns! Meu amigo.";
		mes "Meu nome é 'Brad'. Eu sou responsável de recursos humanos aqui.";
		mes "Eu não tenho certeza por agora, mas você vai ter mais chances de me ver mais tarde.";
		next;
		mes "[Brad]";
		mes "Ok, eu fiz o que eu posso fazer para você, para ir em seu caminho. Eu sou um homem bastante ocupado.";
		mes "Vejamos novamente.";
		close;
	}
	if(Class != Job_Novice) {
		if(Class == Job_Thief) {
			mes "[Brad]";
			mes "Eu não tenho quaisquer eventos especiais agora. Então, vá no seu caminho e voltar mais tarde.";
		}else{
			mes "[Brad]";
			mes "Ei~ Ei~ Você não é um Aprendiz ou um Gatuno!";
			mes "O que você está fazendo aqui? Você não é bem-vindo para este lugar de sua casa~ Hweeeee~ Saia daqui~";
		}
		close;
	}
	mes "[Brad]";
	mes "Hã? Porque é que um novato como você visitando aqui?";
	mes "Se você está aqui para se tornar um Gatuno, pergunte a senhora que está perto de mim.";
	close;
}