//    ________________________________________________________ 
//   /                                                        \
//   |       _           _   _   _                            |
//   |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
//   |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
//   |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
//   |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
//   |                                                        |
//   |                    brAthena Script                     |
//   |--------------------------------------------------------|
//   | Nome do Script: mago.txt                               |
//   |--------------------------------------------------------|
//   | Criado por: eA/rA/brA Dev. Team                        |
//   |--------------------------------------------------------|
//   | Versões eA/rA/brA: 1.9/1.0/1.1                         |
//   |--------------------------------------------------------|
//   | Descrição: Mudança de Classe para Mago.                |
//   |--------------------------------------------------------|
//   | Changelog:                                             |
//   | 1.0 Criado Cabeçalho [Megasantos]                      |
//   | 1.1 Tradução PT-BR [Holy]                              |
//   |--------------------------------------------------------|
//   | - Anotações                                            |
//   \________________________________________________________/

geffen_in,164,124,4	script	Maga Guardiã#maga	123,{
	if (Upper == 1) {
		if (ADVJOB == Job_High_Wizard || ADVJOB == Job_Professor) {
			if (Class == Job_Novice_High) {
				mes "[Maga Guardiã]";
				mes "Olá, faz bastante tempo que não nos vemos! Mas você não deveria estar morto?";
				next;
				mes "[Maga Guardiã]";
				mes "Ah, você deve ter sido renascido. Bem, eu estou contente em vê-lo novamente.";
				next;
				if (getskilllv("NV_BASIC") < 9) {
					mes "[Maga Guardiã]";
					mes "Sinto muito, mas eu acho que você ainda não está pronto para se tornar um Mago. Por que você não termina de aprender as habilidades básicas primeiro?";
					next;
					mes "[Maga Guardiã]";
					mes "Fique à vontade. Quanto mais você aprende, mais você estará pronto para aprender magia.";
					close;
				}
				mes "[Maga Guardiã]";
				mes "Bem, já que que você passou no teste de Mago uma vez, eu não vou questionar a sua qualificação. Você quer ter suas habilidades mágicas de volta imediatamente, não é?";
				next;
				skill 143,0,0;
				jobchange Job_Mage_High;
				skill 157,1,0;
				mes "[Maga Guardiã]";
				mes "Uau, por alguma razão você está bem melhor do que antes. De qualquer forma, eu acredito que você irá fazer um trabalho melhor sendo um Arquimago.";
				close;
			}else{
				mes "[Maga Guardiã]";
				mes "Há algo mais que eu possa ajudá-lo? Se não, por que você não vai testar suas habilidades? O mundo está esperando por você~!";
				close;
			}
		}else{
			mes "[Maga Guardiã]";
			mes "O que, você está interessado na Guilda dos Magos? Eu não queria dizer isso, mas você não pertence a ela.";
			next;
			mes "[Maga Guardiã]";
			mes "Eu não sei por que você ainda está na minha frente, mas eu posso dizer que você não está destinado a ser um Mago.";
			close;
		}
	}
	if(Class != Job_Novice) {
		mes "[Maga Guardiã]";
		if(Class == Job_Mage) {
			mes "Ei, o que faz aqui? Você já é um Mago, bobo!";
			mes "Um dia você irá perceber o poder que existe dentro de você quando puder fazer fogo com sua mente!";
		}else{
			mes "Ei~ Vamos. Pare de bricadeiras. Você não pode ser um Mago porque você já possui outra classe.";
		}
		close;
	}else{
		mes "[Maga Guardiã]";
		mes "Hum?";
		next;
		switch(select("Eu quero me tornar um Mago:Quais são os requerimentos para se tornar um Mago?:Nada, obrigado.")) {
		case 1:
			mes "[Maga Guardiã]";
			mes "Quer ser um Mago? É...";
			next;
			mes "[Maga Guardiã]";
			if (Sex) {
				mes "Ei, olhe para você! Você é bonitinho~! Embora não faça meu tipo...";
			}else{
				mes "Aaah, você é uma garota tão linda~!";
				mes "Eu gosto de garotas como você~";
			}
			mes "Certo, você disse que quer ser um Mago?";
			next;
			if(select("Eu quero me tornar um Mago.:Não, obrigado.") == 2) {
				mes "[Maga Guardiã]";
				mes "Oquuuuue~?! Logo depois que você me diz que quer se tornar um Mago você muda de idéia?! Tenha certeza no que faz!";
				close;
			}
			mes "[Maga Guardiã]";
			if (getskilllv("NV_BASIC") < 9) {
				mes "Ah, seu nível de Habilidades Básicas não atinge o nível suficiente para se tornar um Mago.";
				mes "Volte e atinga o seu nível de habilidades básicas no máximo.";
				close;
			}
			mes "Tudo bem... assine o formulário.... Uau ~ você tem boa caligrafia! Ahh, tudo bem. Basta.";
			mes "Você é... "+strcharinfo(0)+".";
			next;
			mes "[Maga Guardiã]";
			mes "Hum Eu posso ver que você se esforçou no seu próprio caminho. Embora pareça ser um pouco desajeitado, mas bem eu acho que está tudo bem!";
			mes "Bom! Sempre aderir aos princípios básicos é o melhor! Vou transformá-lo imediatamente.";
			next;
			mes "[Maga Guardiã]";
			mes "Haha..! Você agora é um mago, um dos nossos colegas!";
			mes "Recrutamos você para a Guilda dos Magos, nosso novo amigo!";
			next;
			mes "[Maga Guardiã]";
			mes "'Bem-vindo a Guilda de Magos~'";
			mes "Parabéns por se tornar um Membro da Guilda de Magos!";
			callfunc "Job_Change",Job_Mage;
			getitem	1639,1; // Cajado de Iniciantes
			close;
		case 2:
			mes "[Maga Guardiã]";
			mes "Quer ser um Mago, é?";
			mes "Eu ficaria feliz em explicar os requisitos para uma menina bonita como você!";
			next;
			mes "[Maga Guardiã]";
			mes "Primeiro de tudo, você tem que ser uma Aprendiz com Nível de Classe 10 e aprender todas as habilidades básicas.";
			next;
			mes "[Maga Guardiã]";
			mes "No passado, vimos que esse teste estava um pouco complicado. Por causa disso, perdemos mais candidatos.";
			mes "Então, decidimos aceitar todos os candidatos que preencham os requisitos básicos.";
			next;
			mes "[Maga Guardiã]";
			mes "Não hesite. Basta ser um Mago!";
			close;
		case 3:
			mes "[Maga Guardiã]";
			mes "Nada...?";
			close;
		}
	}
}