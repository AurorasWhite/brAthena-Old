//    ________________________________________________________ 
//   /                                                        \
//   |       _           _   _   _                            |
//   |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
//   |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
//   |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
//   |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
//   |                                                        |
//   |                    brAthena Script                     |
//   |--------------------------------------------------------|
//   | Nome do Script: espadachim.txt                         |
//   |--------------------------------------------------------|
//   | Criado por: eA/rA/brA Dev. Team                        |
//   |--------------------------------------------------------|
//   | Versões eA/rA/brA: 2.3/1.0/1.1                         |
//   |--------------------------------------------------------|
//   | Descrição: Mudança de Classe para Espadachim.          |
//   |--------------------------------------------------------|
//   | Changelog:                                             |
//   | 1.0 Criado Cabeçalho [Megasantos]                      |
//   | 1.1 Tradução PT-BR [Holy]                              |
//   |--------------------------------------------------------|
//   | - Anotações                                            |
//   \________________________________________________________/

izlude_in,74,172,4	script	Chefe Espadachim#swd	119,{
	if (Upper == 1) {
		if (Class == Job_Novice_High && (ADVJOB == Job_Lord_Knight || ADVJOB == Job_Paladin)) {
			mes "[Chefe Espadachim]";
			mes "Isso...";
			mes "Não pode ser...";
			mes "Você renasceu, não foi?";
			next;
			mes "[Chefe Espadachim]";
			mes "Saiba que está se entregando ao caminho dos Espadachins! Uma vez que você se acostumou a brandir uma espada, você nunca poderá voltar atrás!!";
			next;
			if (getskilllv("NV_BASIC") < 9) {
				mes "[Chefe Espadachim]";
				mes "Hum? Ah, você deve primeiro dominar as habilidades básicas antes de estar pronto para se tornar um Espadachim.";
				next;
				mes "[Chefe Espadachim]";
				mes "Volte aqui quando terminar de aprender as habilidades básicas de Aprendiz.";
				close;
			}
			mes "[Chefe Espadachim]";
			mes "Excelente! Deixe-me promovê-lo a um Espadachim imediatamente!";
			next;
			skill 143,0,0;
			jobchange Job_Swordman_High;
			skill 144,1,0;
			skill 145,1,0;
			skill 146,1,0;
			mes "[Chefe Espadachim]";
			mes "Hum... Você parece um Espadachim bem experiente. Ainda assim, eu tenho certeza que você deve treinar para melhorar suas habilidades e ganhar força!";
			close;
		}else{
			mes "[Chefe Espadachim]";
			mes "Hum...?";
			mes "Você é um guerreiro";
			mes "renascido, não é?";
			next;
			mes "[Chefe Espadachim]";
			mes "Humm...";
			mes "Parece que ser";
			mes "um Espadachim não é parte";
			mes "do seu destino. Me desculpe,";
			mes "mas não há nada que possa";
			mes "fazer por você.";
			close;
		}
	}
	mes "[Chefe Espadachim]";
	mes "Essa é a Guilda de Espadachins.";
	mes "O que você deseja?";
	next;
	switch(select("Me fale como se tornar um Espadachim.:Eu quero me tornar um Espadachim.:Nada.")) {
	case 1:
		mes "[Chefe Espadachim]";
		mes "Então você deseja saber mais sobre o poderoso Espadachim? Ok!";
		next;
		mes "[Chefe Espadachim]";
		mes "A característica mais marcante do Espadachim é que o Espadachim pode nos mostrar os seus / suas reais conhecimentos ecapacidades no combate corpo a corpo.";
		mes "Há três razões!";
		next;
		mes "[Chefe Espadachim]";
		mes "Primeiro, Espadachins possuem o maior HP dentre todas as classes.";
		mes "Segundo, exceto por Arcos e Cajados, o Espadachim pode usar todas as outras armas para que possam lutar em sua capacidade ideal.";
		mes "E Terceiro, a maioria das habilidades do Espadachim são poderosos ataques físicos.";
		next;
		mes "[Chefe Espadachim]";
		mes "Embora eu tenha lhe dado uma explicação simples, eu acredito que você entendeu o significado central do que é ser um Espadachim.";
		mes "Na minha opinião, o Espadachim é a mais forte das classes!";
		close;
	case 2:
		if(Class != Job_Novice) {
			mes "[Chefe Espadachim]";
			if(Class == Job_Swordman) {
				mes "Você já é um excelente Espadachim, não é?";
				mes "Apenas dedique-se a isso, seja um grande Espadachim.";
			}else{
				mes "Você já possui uma outra classe, não é?";
				mes "Você foi longe demais com essa piada.";
			}
			close;
		}else{
			if (getskilllv("NV_BASIC") < 9) {
				mes "[Chefe Espadachim]";
				mes "Sinto muito dizer em isso, mas para ser um Espadachim você deve possuir pelo menos ^4A4AFFNível de Classe 10^000000.";
				mes "e ^4A4AFFNível de Habilidades Básicas 9^000000.";
				next;
				mes "[Chefe Espadachim]";
				mes "Quer ser um espadachim sem ter o requisito mínimo?";
				mes "Você acha que ser um Espadachim é fácil?";
				close;
			}
			mes "[Chefe Espadachim]";
			mes "Hum, Verifique tanto o nível de classe como seu nível de habilidades básicas.";
			mes "Bom. Você quer ser um Espadachim agora?";
			next;
			if(select("Sim, eu quero.:Eu vou pensar mais um pouco.") == 2) {
				mes "[Chefe Espadachim]";
				mes "Sim. Uma decisão prudente é necessária para se escolher uma classe.";
				mes "Mas eu sinto muito... Você está voltando atrás depois de superar todas as dificuldades....";
				close;
			}
			mes "[Chefe Espadachim]";
			mes "Parabéns! De agora em diante, você irá viver a vida de um Espadachim!";
			mes "Vamos fazer isso agora!";
			next;
			mes "[Chefe Espadachim]";
			mes "Parabéns mais uma vez por ser um Espadachim e espero que você participe de muitas atividades para renascer a nossa Guilda.";
			callfunc "Job_Change",Job_Swordman;
			getitem 13415,1; // Alfanje de Iniciante
			close;
		}
	case 3:
		close;
	}
}