//    ________________________________________________________
//   /                                                        \
//   |       _           _   _   _                            |
//   |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
//   |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
//   |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
//   |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
//   |                                                        |
//   |                    brAthena Script                     |
//   |--------------------------------------------------------|
//   | Nome do Script: malangdo.txt                           |
//   |--------------------------------------------------------|
//   | Criado por: eA/rA/brA Dev. Team                        |
//   |--------------------------------------------------------|
//   | Vers?es eA/rA/brA: ??/1.4/1.3                          |
//   |--------------------------------------------------------|
//   | Descrição: NPCs da Cidade de Malangdo.                 |
//   |--------------------------------------------------------|
//   | Changelog:                                             |
//   | 1.0 Criado Cabeçalho [Megasantos]                      |
//   | 1.1 Otimização [Hold & Holy]                           |
//   | 1.2 Tradução [RoDrIgO]                                 |	
//   | 1.3 Revisão [Gordis]                                   | 	
//   |--------------------------------------------------------|
//   | - Anotações                                            |
//   \________________________________________________________/

// Ida para Ilha Malangdo
//============================================================
prontera,114,77,6	script	Dolangmal	553,{
	if (checkquest(5091) == -1) {
		mes "[Dolangmal]";
		mes "Eii~! Olá humano, eu sou o Sr. Dolangmal. kakaka~";
		next;
		mes "[Dolangmal]";
		mes "Nós fomos enviados aqui para lhe levar ao paraíso dos gatos, ^A2314BA Ilha dos Gatos^000000.";
		next;
		mes "[Dolangmal]";
		set .@s$,"Por que você não vai para ^A2314BIlha dos Gatos^000000? ";
		switch(atoi(strnpcinfo(2))) {
			case 1: mes "Pense Bem! Tem muita gente em Prontera, por que você não vem relaxar no paraíso dos gatos, ^A2314BA Ilha dos Gatos^000000?"; break;
			case 2: mes "Por que você não vai para a ^A2314BIlha dos Gatos^000000? lá é melhor que geffen que está lotada de bruxos."; break;
			case 3: mes "O ar fresco de payon não é tão ruim, mas por que você não vem para o paraíso dos gatos, ^A2314BA Ilha dos Gatos^000000?"; break;
			case 4: mes .@s$+"Lá é bonito e silencioso. É melhor que Aldebaran que é tão barulhenta com os ruídos do relógio."; break;
			case 5: mes .@s$+"Lá é bonito e silencioso. É melhor que Morroc que é tão quente e tem tempestades de areia."; break;
			case 6: mes .@s$+"Lá é muito bonito. É melhor que Juno que é cheia de livros que dão dor de cabeça."; break;
			case 7: mes .@s$+"Lá é muito bem povoado, diferente de Rachel que é terrivelmente silenciosa."; break;
			case 8: mes .@s$+"Lá é um lugar seguro, é melhor que Lighthalzen que muito perigosa."; break;
			case 9: mes .@s$+"O cheiro que vem do mar é muito melhor que Mora que cheira a grama."; break;
		}
		next;
		if(select("Não:^A2314BIlha dos Gatos^000000~ Vamos~ Vamos~") == 1) {
			mes "[Dolangmal]";
			mes "O que!? você Não quer ir para a ^A2314BIlha dos Gatos^000000? Chet!";
			next;
			mes "[Dolangmal]";
			mes "Você não passa de mais um garoto irritante. Se você não vai, por que está falando comigo...";
			next;
			mes "[Dolangmal]";
			mes "^9E1837Almirante Thomas^000000 Disse para raptar os huma.. Não.. Trazer eles educadamente.";
			next;
			mes "[Dolangmal]";
			mes "De qualquer forma, eu respeito sua decisão. Quando quiser ir é só me dizer.";
			close;
		}
		mes "[Dolangmal]";
		mes "Ok! Eu gosto dessa sua atitude cheia de paixão.";
		next;
		mes "[Dolangmal]";
		mes "Agora que você vai para a ^A2314BIlha dos Gatos^000000, Deixe me dizer você alguns cuidados que você deve tomar.";
		next;
		mes "[Dolangmal]";
		mes "Primeiro!! Essa é uma coisa natural. Quando você desembarcar na ^A2314BIlha dos Gatos^000000, você deve notificar que você está lá para ver o ^9E1837Almirante Thomas^000000..";
		next;
		mes "[Dolangmal]";
		mes "Se você não fizer isso, você pode ser atacado pelos gatos assustadores e selvagens, enquanto anda pela estrada.";
		next;
		mes "[Dolangmal]";
		mes "Segundo!! Essa ^A2314BIlha dos Gatos^000000 é absolutamente para os gatos, apezar de estarmos abrindo para visitantes humanos.";
		next;
		mes "[Dolangmal]";
		mes "Nós temos uma personalidade... Não... Não... nós temos respeito mútuo, nos respeite e será respeitado.";
		next;
		mes "[Dolangmal]";
		mes "E o próximo é...";
		next;
		mes "Dolangmal disse que seriam alguns cuidados, mas ele disse mais de 100 tipos de precauções.";
		next;
		mes "[Dolangmal]";
		mes "Ok! Isso é tudo com o que precisa tomar cuidado!! É facil e simples... Que tal? Você pode tomar todos esses cuidados?";
		next;
		switch(select("Vou tomar esses cuidados:Não conseguirei tomar todos esses cuidados")) {
		case 1:
			mes "[Dolangmal]";
			mes "Ok! Não morda dois ratos com uma boca só. O ^9E1837Almirante Thomas^000000 está localizado no ^A2314BCentro do navio na Ilha dos Gatos^000000.";
			next;
			mes "[Dolangmal]";
			mes "Não esqueça dar notícia ao chegar.";
			next;
			mes "[Dolangmal]";
			mes "Ok. vamos para o céu do gatos, ^A2314BIlha dos Gatos^000000~";
			setquest 5091;
			completequest 5091;
			warp "malangdo",217,85;
			close;
		case 2:
			mes "[Dolangmal]";
			mes "O que!? Você não pode manter essa simples regra?";
			next;
			mes "[Dolangmal]";
			mes "Você não passa de mais um garoto irritante. Se você não vai, por que está falando comigo..";
			next;
			mes "[Dolangmal]";
			mes "^9E1837Almirante Thomas^000000 Disse para raptar os huma.. Não.. Trazer eles educadamente.";
			next;
			mes "[Dolangmal]";
			mes "De qualquer forma, eu respeito sua decisão. Quando quiser ir é só me dizer.";
			close;
		}
	}
	mes "[Dolangmal]";
	mes "Hã? Você quer visitar a ^A2314BIlha dos Gatos^000000?";
	next;
	switch(select("Leve me para a ^A2314BIlha dos Gatos^000000 novamente:Eu já visitei")) {
	case 1:
		mes "[Dolangmal]";
		mes "Eu realmente gostaria de lhe enviar para lá, mas eu não posso por causa das nossas políticas, é difícil enviar pessoas para visitar, a ^A2314BIlha dos Gatos^000000 mais de uma vez.";
		next;
		mes "[Dolangmal]";
		mes "E se você ouviu o que eu disse... então você deve se lembrar da regra 53...";
		next;
		mes "[Dolangmal]";
		mes "Aliás, Deixe me explicar regra 53. Eu sou um gato que perdoa facilmente.";
		next;
		mes "[Dolangmal]";
		mes "Se você quer ir para a ^A2314BIlha dos Gatos^000000, basta pegar navio em Izlude ou Alberta.";
		next;
		mes "[Dolangmal]";
		mes "Se você gosta de viajar pelo mar, você irá desembarcar na ^A2314BIlha dos Gatos^000000.";
		next;
		mes "[Dolangmal]";
		mes "Tão fácil! Não é? Bem, adeus~";
		close;
	case 2:
		mes "[Dolangmal]";
		mes "Que tal? Você gostou de lá? Eu já sei a resposta. Hahaha";
		next;
		mes "[Dolangmal]";
		mes "Ah! Eu só para ter certeza..";
		next;
		mes "[Dolangmal]";
		mes "Se você quer ir para a ^A2314BIlha dos Gatos^000000, você tem que pegar o navio em Izlude ou Alberta!";
		next;
		mes "[Dolangmal]";
		mes "Hum~ você precisa saber disso porque você é uma pessoa legal. Bem, fico feliz em ver você denovo~";
		close;
	}
}
geffen,109,61,6	duplicate(Dolangmal)	Dolangmal#2	553
payon,200,106,4	duplicate(Dolangmal)	Dolangmal#3	553
aldebaran,170,104,4	duplicate(Dolangmal)	Dolangmal#4	553
morocc,168,275,4	duplicate(Dolangmal)	Dolangmal#5	553
yuno,165,122,4	duplicate(Dolangmal)	Dolangmal#6	553
rachel,121,126,4	duplicate(Dolangmal)	Dolangmal#7	553
lighthalzen,254,83,5	duplicate(Dolangmal)	Dolangmal#8	553
mora,57,150,4	duplicate(Dolangmal)	Dolangmal#9	553

izlude,182,218,4	script	Odgnalam#Izlude	554,{
	mes "Recebendo o sol quente de "+strnpcinfo(2)+".";
	next;
	if(select("Deixe ele durmir:Fale com ele") == 1) close;
	mes "[Odgnalam]";
	mes "Miau? Hã?";
	next;
	if (ep13_yong1 == 0) {
		set .@price,1000;
		set .@s1$,"Hã!! Você é uma pessoa irritante. Deixe eu lhe enviar para a ^A2314BIlha dos Gatos^000000 por ^0000FF1000 Zenys^000000.";
		set .@s2$,"Dinheiro? Onde está o dinheiro? você não vai à lugar algum sem o dinheiro. Tenha vergonha de si mesmo.";
	} else if (ep13_yong1 < 60) {
		set .@price,999;
		set .@s1$,"Você tem alguma afinidade com a frota dos gato. Eu vou dar a você um desconto especial de ^0000FF999 Zenys^000000 para o paraíso do gatos, ^A2314BIlha dos Gatos^000000, por que você tem amigos na frota dos gatos.";
		set .@s2$,"Onde está o dinheiro? Você não tem um senso de honra.";
	} else if (ep13_yong1 > 59 && ep13_yong1 < 80) {
		set .@price,500;
		set .@s1$,"Você foi de grande ajuda para a frota dos gatos, então você tem 50% de desconto para ir à Ilha dos Gatos. Eu vou enviar você para a ^A2314BIlha dos Gatos^000000.";
		set .@s2$,"Você não tem dinheiro nem para pagar 50% de desconto? Eu não acredito nisso.";
	} else {
		mes "[Odgnalam]";
		mes "Você é o herói da frota dos gatos, uma lenda entre nós.";
		next;
		mes "[Odgnalam]";
		mes "Já que você é o herói da frota então vou lhe enviar para a ^A2314BIlha dos Gatos^000000 gratuitamente.";
		next;
		if(select("Vamos para Ilha dos Gatos!!:Não quero ir.") == 1) {
			mes "[Odgnalam]";
			mes "Tenha uma viagem confortável... Ok, vamos nessa~";
			warp "malangdo",217,85;
		}
		close;
	}
	mes "[Odgnalam]";
	mes .@s1$;
	next;
	if(select("Vamos para Ilha dos Gatos!:Não quero ir.") == 2) close;
	if (Zeny < .@price) {
		mes "[Odgnalam]";
		mes "Dinheiro? Onde está o dinheiro? você não vai a qualquer lugar sem o dinheiro. Tenha vergonha de si mesmo.";
		close;
	}
	set Zeny, Zeny - .@price;
	mes "[Odgnalam]";
	mes "Deixe eu lhe enviar para lá~";
	warp "malangdo",217,85;
	close;
}
alberta,200,151,4	duplicate(Odgnalam#Izlude)	Odgnalam#Alberta	554

malangdo,219,86,4	script	Kong#malang	545,{
	mes "[Kong]";
	mes "Você está se divertindo em Malangdo?";
	mes "A frota tem serviços em Izlude e Alberta.";
	next;
	mes "[Kong]";
	if ( !ep13_yong1 || ep13_yong1 < 60 ) {
		set .@price,(( !ep13_yong1 ) ? 1000 : 999);
		set .@s1$, ""+(( !ep13_yong1 ) ? "1000" : "999")+" zeny";
		set .@s2$, ""+(( !ep13_yong1 ) ? "Tenha um bom dia na Ilha dos Gatos":"Tenha um dia de sorte na Ilha dos Gatos...")+"";
		set .@no_money$, ""+(( !ep13_yong1 ) ? "É difícil conseguir uma viagem de graça.":"É difícil conseguir uma viagem de graça se você não tem nenhum relacionamento com as tropas")+".";
		set .@yes_money$, ""+(( !ep13_yong1 ) ? "Tenha uma boa viagem." : "Obrigado, tenha uma viagem maravilhosa")+".";
		mes ""+(( !ep13_yong1 ) ? "O custo da passagem é de 1000 zenys, aonde quer ir" : "Você tem uma conexão com a frota. Onde você deseja ir")+"?";
	}	else if ( ep13_yong1 > 59 && ep13_yong1 < 80 || ep13_yong1 > 80 ) {
		set .@price,(( ep13_yong1 > 59 && ep13_yong1 < 80 ) ? 500 : 0);
		set .@s1$,""+(( ep13_yong1 > 59 && ep13_yong1 < 80 ) ? "500 zeny" : "De graça para o Herói da frota!!")+"";
		set .@s2$, ""+(( ep13_yong1 > 59 && ep13_yong1 < 80 ) ? "Tenha muita sorte e dinheiro na Ilha dos Gatos." : "Nenhum nega o herói da frota a sair da Ilha dos Gatos...")+"";
		if ( ep13_yong1 > 59 && ep13_yong1 < 80 ) {
		set .@no_money$, "Ah meu deus. Você não tem 500 zenys? Não dá pra viajar de graça.";
		set .@yes_money$, "Tenha uma boa viagem e volte sempre.";
		}
		mes ""+(( ep13_yong1 > 59 && ep13_yong1 < 80 ) ? "Uau~ Você contribuiu muito com a frota. Ok!! Você tem um desconto de 500 zeny!" : "Ah... você é o o herói da frota dos gatos, você é lendário.")+"";
		next;
		if ( ep13_yong1 > 80 ) {
		mes "[Kong]";
		mes "Nós não cobramos nada do herói da frota. Qual é o seu destino?";
		next;
		}
	set .@i, select("- Izlude --- "+.@s1$+":- Alberta --- "+.@s1$+":- Ficar em Malangdo");
	mes "[Kong]";
	if (.@i == 3) {
		mes .@s2$;
		close;
	}
	if (ep13_yong1 > 80) {
		mes "Tenha uma viagem confortável.";
		warp "((.@i == 1) ? izlude : alberta)",((.@i == 1) ? 195 : 192),((.@i == 1) ? 212 : 150);
		close;
	}
	if (Zeny < .@price) {
		mes .@no_money$;
		close;
	}
	set Zeny, Zeny - .@price;
	mes .@yes_money$;
	warp "((.@i == 1) ? izlude : alberta)",(.@i == 1) ? 195 : 192,((.@i == 1) ? 212 : 150);
	close;
}
}

// NPCs da Cidade
//============================================================
malangdo,147,117,3	script	Recepcionista#malang	554,{
	mes "Você vê um gato preguiçoso sentado em uma cadeira.";
	mes "Embora seja um Recepcionista, não parece querer negócios.";
	next;
	switch(select("Posso descançar na rede?:Preciso guardar algumas coisas.:Estou livre no momento.")) {
	case 1:
		mes "[Recepcionista]";
		mes "Você pode usar a rede, mas precisa me dar ^FF00005 Peixes Enlatados de Malangdo^000000, para você poder desestressar.";
		next;
		if(select("Dar as latas.:Não preciso.") == 1) {
				mes "[Recepcionista]";
			if (countitem(12636) > 4) {
				delitem 12636,5; // Peixes Enlatados de Malangdo
				percentheal 100,100;
				specialeffect2 EF_HEALSP;
				mes "Relaxe.";
				emotion e_kis;
				next;
				warp "malangdo",140,121;
				close;
			}
			mes "Parece que ele tem várias latas..";
			close;
			}
			mes "[Recepcionista]";
			mes "Você é um homem mais forte do que aparenta.";
			close;

	case 2:
		mes "[Recepcionista]";
		mes "Terminei de armazenar, nós deviamos ser companheiros, Haha.";
		savepoint "malangdo",142,118;
		close;
	case 3:
		mes "[Recepcionista]";
		mes "Um momento de correria faz você ágil e um momento de relax o faz preguiçoso.";
		next;
		mes "- Andarilhos aparecem por aqui de vez em quando. -";
		close;
	}
	close;
}

malangdo,151,120,3	script	Menestrel Andarilho#mal	479,{
	mes "[Menestrel]";
	mes "Eu sinto sono sempre que vejo o Recepcionista. Tão confortável.";
	next;
	mes "[Menestrel]";
	mes "Ele faz a vida parecer tão simples.";
	close;
}

malangdo,149,120,5	script	Musa Andarilha#mal	485,5,1,{
	mes "[Musa Andarilha]";
	mes "Os gatos que vivem aqui são tão energéticos.";
	mes "Isso me faz dançar~";
	close;
OnTouch:
	set .@rand, (rand(5));
	if ( !.@rand)
		soundeffect "ring_of_nibelungen.wav",0;
	else if ( .@rand == 1)
		soundeffect "dont_forget_me_not.wav",0;
	else if ( .@rand == 2)
		soundeffect "humming.wav",0;
	else if ( .@rand == 3)
		soundeffect "assassin_of_sunset.wav",0;
	else if ( .@rand == 4) {
		emotion e_ho;
		emotion e_ho,0,"Musa Andarilha#mal";
	}
	end;
}

malangdo,216,168,3	script	Treinadora de Gatos#mal1	558,0,8,{
	emotion e_awsm;
	mes "[Treinadora de Gatos]";
	mes "Pff~ Pelos lisos e macios, adoro gatinhos!";
	mes "Não aguento, isso é o paraíso~~!!";
	next;
	emotion e_gg;
	mes "[Treinadora de Gatos]";
	mes "Minha carta na manga ^FF0000Fruta dos Gatos^000000 pode fazer todos os gatos dessa ilha se tornarem meus escravos ~ haha!";
	next;
	select("^FF0000<Fruta dos Gatos>^000000?");
	mes "[Treinadora de Gatos]";
	mes "A fruta dos Gatos é o item mais raro!";
	mes "Quase não consigo colhe-la.";
	mes "Mas nenhum gato consegue resistir a ela.";
	next;
	select("Onde eu posso conseguir a ^FF0000Fruta dos Gatos^000000?");
	mes "[Treinadora de Gatos]";
	mes "Você não entendeu, é um item muito raro, me esforcei muito para conseguir uma.";
	mes "Isso não pode ser vendido nem mesmo por todo o Zeny deste mundo.!";
	next;
	emotion e_gg;
	mes "[Treinador de Gatos]";
	mes "Os gatos aqui da ilha são meus...! E isso é tudo haha~";
	mes "^FF0000<Fruta dos Gatos>^000000! Eu posso ser a rainha da ilha~ ohhhh!!!";
	close;
OnTouch:
	emotion e_flash;
	end;
}

malangdo,164,203,3	script	Cat Trainer#mal2	853,{
	emotion e_awsm;
	mes "[Cat Trainer]";
	mes "Ooo~ their soft puffy tails and soft fur~";
	mes "I can't stand it, this is heaven~~!!";
	next;
	emotion e_gg;
	mes "[Cat Trainer]";
	mes "This is my little secret called";
	mes "^FF0000Silvervine Fruit^000000. I can use";
	mes "it to make the Malangdo cats my";
	mes "slaves~ muahahahaha~!";
	next;
	select("^FF0000Silvervine Fruit^000000?");
	mes "[Cat Trainer]";
	mes "Hmm, I guess it's no wonder you don't know.";
	mes "These Malangdo cats can't resist it.";
	mes "But it's too rare of an item to get so you shouldn't bother trying to find it.";
	next;
	select("Where can I get ^FF0000Silvervine Fruit^000000?");
	mes "[Cat Trainer]";
	mes "Actually, it's simple. You just...";
	next;
	mes "[Cat Trainer]";
	mes "Hey, you almost made me tell my secret.";
	next;
	emotion e_gg;
	mes "[Cat Trainer]";
	mes "One day these cats will be mine.";
	mes "I can be a king in Malangdo~ ohhh~!";
	close;
}

// Utility NPCs :: cat_merchant
//============================================================
mal_in02,134,31,5	script	Investment Cat Helper	505,{
	if (MaxWeight - Weight < 100 || checkweight(1201,1) == 0) {
		mes "[Investment Cat Helper]";
		mes "You have a heavy load. Maybe you're a peddler? No peddlers in this area.";
		close;
	}
	if (ep13_yong1 < 10) {
		mes "[Investment Cat Helper]";
		mes "Ohh!";
		mes "You've come here to ask about investing?";
		mes "You've come to the right place.";
		next;
		mes "[Investment Cat Helper]";
		mes "I am here to invest in the Cat Paw Merchant Association.";
		mes "Err-- I mean I'm here to help honest people like you.";
		mes "Hehehe~";
		next;
		mes "[Investment Cat Helper]";
		mes "I can't remember everything because I've been here talking to you humans.";
		mes "But I want to know first... Hold on.";
		next;
		mes "smell... smell...";
		next;
		mes "[Investment Cat Helper]";
		mes "Ah?!";
		mes "You don't know much about the Cat Paw Merchant Association, do you?";
		mes "Hmm, you do not really smell like a cat.";
		next;
		mes "[Investment Cat Helper]";
		mes "Good!!";
		mes "You made the right decision to be here to invest in the Cat Paw Merchant Association~";
		next;
		mes "[Investment Cat Helper]";
		mes "First, you need to have ten pieces of Malangdo cans if you want to open an account.";
		next;
		while(1) {
			switch(select("Open an account:Ask about benefits:Cancel")) {
			case 1:
				if (countitem(12636) < 10) {
					mes "[Investment Cat Helper]";
					mes "Hmm!";
					mes "I'd love to give you account for";
					mes "free but as you know, this is for";
					mes "advertising investers of the trader group, sorry.";
					close;
				}
				delitem 12636,10; //Malang_Sp_Can
				set ep13_yong1,10;
				mes "[Investment Cat Helper]";
				mes "Good!";
				mes "Write down here~ and";
				mes "print your palm there,";
				mes "ah!~ what's your name?";
				next;
				mes "[Investment Cat Helper]";
				mes strcharinfo(0)+" you are~";
				next;
				mes "[Investment Cat Helper]";
				mes "Name: "+strcharinfo(0);
				mes "Number of accounts: 10";
				mes "OK! Registered!";
				next;
				mes "[Investment Cat Helper]";
				mes "You have an account now.";
				mes "Will get an investment of a certain nunber of cans next time.";
				next;
				mes "[Investment Cat Helper]";
				mes "I wish to get lots of investments";
				mes "so you can have many services as well.";
				mes "And, I also hope that we, the Cat Paw Merchant Association, will make much money.";
				next;
				mes "[Investment Cat Helper]";
				mes "See you next investment~";
				close;
			case 2:
				mes "[Investment Cat Helper]";
				mes "You want to know the benefits of investing?";
				mes "Actually, we are a small group of cats and we don't think like humans.";
				next;
				mes "[Investment Cat Helper]";
				mes "There are lots of benefits based on how much you've invested in our services.";
				mes "These include more warps, extra storage locations and price reductions of goods and services.";
				next;
				mes "[Investment Cat Helper]";
				mes "I promise that if our trade is growing you will benefit from it as well.";
				next;
				break;
			case 3:
				mes "[Investment Cat Helper]";
				mes "Ahh~ You're just going to leave?";
				mes "T_T~ Please open an account.";
				close;
			}
		}
	} else if (ep13_yong1 < 20000) {
		mes "[Investment Cat Helper]";
		mes "I can't remember everyone since I meet so many people...";
		mes "But I want to check first... Hold on.";
		mes "Excuse me!";
		next;
		mes "smell... smell...";
		next;
		if (ep13_yong1 < 10000) {
			set .@cans,10;
			mes "[Investment Cat Helper]";
			mes "This is a familiar smell!!";
			mes "If it's so...!!";
			next;
			mes "[Investment Cat Helper]";
			mes "You are here to invest!!";
			mes "Good!~~ Good to see you~";
			next;
			mes "[Investment Cat Helper]";
			mes "From your smell~ you are below 10,000 accounts.";
			mes "Until then, you can get an account by investing ten cans.";
		} else {
			set .@cans,20;
			mes "[Investment Cat Helper]";
			mes "Such a stong smell!!";
			mes "Then it must be!!";
			next;
			mes "[Investment Cat Helper]";
			mes "A VIP invester!!!!!!";
			mes "You're here to invest? Welcome!";
			next;
			mes "[Investment Cat Helper]";
			mes "A VIP invester with over 10,000 accounts";
			mes "can get another account with 20 cans.";
			mes "A good investment!";
		}
		next;
		mes "[Investment Cat Helper]";
		mes "What will you do?";
		next;
		switch(select("Invest:Check the number of accounts:Cancel")) {
		case 1:
			mes "[Investment Cat Helper]";
			mes "Ohh~ thank you for investing~";
			mes "You can get a maximum of 10 accounts at one time.";
			mes "Isn't it lovely?";
			mes "How many will you invest in? Enter 0 to cancel.";
			next;
			input .@input;
			if (.@input == 0) {
				mes "[Investment Cat Helper]";
				mes "Please invest next time~";
				close;
			} else if (.@input > 10) {
				mes "[Investment Cat Helper]";
				mes "You can get a maximum of 10 accounts~";
				close;
			}
			set .@required, .@input*.@cans;
			mes "[Investment Cat Helper]";
			mes "Total " + .@input + " accounts, right?";
			mes "The number of cans you need is "+.@required+" pieces.";
			mes "Do you want to go through with this?";
			next;
			if(select("Yes:No") == 2) {
				mes "[Investment Cat Helper]";
				mes "Oh? Is that wrong? Weird.";
				mes "Please check again~";
				close;
			}
			if (countitem(12636) < .@required) {
				mes "[Investment Cat Helper]";
				mes "Ahhh!";
				mes "You don't have enough cans.";
				mes "I'm a busy cat, you know.";
				close;
			}
			if (ep13_yong1 < 10000) {
				delitem 12636,.@required; //Malang_Sp_Can
				set ep13_yong1, ep13_yong1+.@input;
				mes "[Investment Cat Helper]";
				mes .@input+" accounts were invested in.";
				mes "Thank you for your help~";
				next;
				mes "[Investment Cat Helper]";
				mes "Once we get more investments of cans,";
				mes "our trader group will grow even more.";
				mes "And you can get services as well~";
				mes "A win-win strategy?";
				close;
			} else {
				if (.@input + ep13_yong1 > 20000) {
					delitem 12636,.@required; //Malang_Sp_Can
					set ep13_yong1,20000;
					mes "[Investment Cat Helper]";
					mes "You can have 20,000 accounts max";
					mes "according to the trader policy...";
					mes strcharinfo(0)+"'s accounts increase to 20,000.";
					mes "Thank you so much for your investment~";
					next;
					mes "[Investment Cat Helper]";
					mes "Now, "+strcharinfo(0)+", you can't invest further";
					mes "but you can still donate some of your cans to the trader group.";
					mes "Thank you so much~";
					close;
				} else {
					delitem 12636,.@required; //Malang_Sp_Can
					set ep13_yong1, ep13_yong1+.@input;
					mes "[Investment Cat Helper]";
					mes .@input+" accounts have been invested in.";
					mes "Thank you for your help~";
					next;
					mes "[Investment Cat Helper]";
					mes "With your big investment in cans,";
					mes "this will help us meet our goals earlier.";
					mes "Thank you again.";
					close;
				}
			}
		case 2:
			mes "[Investment Cat Helper]";
			mes "You want to check the number of accounts? Hmm, let's see~";
			next;
			mes "[Investment Cat Helper]";
			mes "Name: "+strcharinfo(0);
			mes "Number of accounts: "+ep13_yong1;
			next;
			mes "[Investment Cat Helper]";
			if (ep13_yong1 < 10000)
				mes "Got it?";
			else
				mes "You've made many investments. Thank you.";
			close;
		case 3:
			mes "[Investment Cat Helper]";
			mes "Please invest next time~";
			close;
		}
	} else if (ep13_yong1 >= 20000) {
		mes "[Investment Cat Helper]";
		mes strcharinfo(0) + ", welcome~";
		mes strcharinfo(0) + "~ I felt something upon your arrival.";
		next;
		mes "[Investment Cat Helper]";
		mes "Want to donate?";
		next;
		if(select("Donate:No") == 2) {
			mes "[Investment Cat Helper]";
			mes "Hehe~ then have fun in here~";
			close;
		}
		mes "[Investment Cat Helper]";
		mes "Wow, thank you, "+strcharinfo(0)+"~!";
		next;
		mes "[Investment Cat Helper]";
		mes strcharinfo(0)+"~ with your help";
		mes "the cans will be distributed to cats";
		mes "in the Midgard area with honor~";
		next;
		mes "[Investment Cat Helper]";
		mes "The can donation can range from a minimum of 20 to a maximum of 10,000.";
		next;
		input .@input;
		if (.@input < 20 || .@input > 10000) {
			mes "[Investment Cat Helper]";
			mes "The can donation can range from a minimum of 20 to a maximum of 10,000.";
			close;
		} else if (countitem(12636) < .@input) { // Malang_Sp_Can
			mes "[Investment Cat Helper]";
			mes "Hmm? You don't have enough cans.";
			close;
		} else {
			delitem 12636,.@input; //Malang_Sp_Can
			mes "[Investment Cat Helper]";
			mes "Ohh! Thank you~";
			next;
			mes "[Investment Cat Helper]";
			mes "Dear "+strcharinfo(0)+" ~";
			mes "I have only Increase AGI";
			mes "and Blessing to give you.";
			mes "Thank you.";
			npcskill "AL_INCAGI",10,0,0;
			npcskill "AL_BLESSING",10,0,0;
			close;
		}
	}
}

malangdo,184,139,4	script	Storekeeper#mal	560,{
	mes "[Storekeeper]";
	mes "Hey, looks like you might be looking to unload some of your stuff from your pack.";
	mes "Want to open storage? For free?";
	next;
	mes "[Storekeeper]";
	mes "Hahaha~ Just kidding, of course it isn't free.";
	mes "What do you take me for?";
	next;
	if (ep13_yong1 > 999) {
		set .@cans,1;
		set .@zeny,1500;
		set .@i, select("Pay with Can ----- 1 unit:Pay with Zeny ------- 1,500z");
	} else {
		set .@cans,2;
		set .@zeny,3000;
		set .@i, select("Pay with Can ----- 2units:Pay with Zeny ------- 3,000z");
	}
	if (getskilllv("NV_BASIC") < 6) {
		mes "[Storekeeper]";
		mes "Ah, hold on!!";
		mes "You need to have ^FF0000Basic Skill level 6^000000 to use the storage service!!";
		close;
	}
	switch(.@i) {
	case 1:
		if (countitem(12636) < .@cans) {
			mes "[Storekeeper]";
			mes "You don't have enough cans...";
			close;
		}
		delitem 12636,.@cans; //Malang_Sp_Can
		break;
	case 2:
		if (Zeny < .@zeny) {
			mes "[Storekeeper]";
			mes "You don't have enough zeny...";
			close;
		}
		set Zeny, Zeny-.@zeny;
		break;
	}
	mes "[Storekeeper]";
	mes "Thank you.";
	close2;
	openstorage;
	end;
}
