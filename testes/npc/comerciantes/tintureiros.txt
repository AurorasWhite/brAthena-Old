//    ________________________________________________________
//   /                                                        \
//   |       _           _   _   _                            |
//   |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
//   |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
//   |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
//   |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
//   |                                                        |
//   |                    brAthena Script                     |
//   |--------------------------------------------------------|
//   | Nome do Script: tintureiros.txt                        |
//   |--------------------------------------------------------|
//   | Criado por: eA/rA/brA Dev. Team                        |
//   |--------------------------------------------------------|
//   | Versões eA/rA/brA: 1.4/1.4/1.1                         |
//   |--------------------------------------------------------|
//   | Descrição: Criador de Tinturas.                        |
//   |--------------------------------------------------------|
//   | Changelog:                                             |
//   | 1.0 Criado Cabeçalho [Shiraz]                          |
//   | 1.1 Tradução PT-BR [Holy]                              |
//   |--------------------------------------------------------|
//   | - Anotações                                            |
//   \________________________________________________________/

morocc_in,146,99,3	script	Java Dullihan	58,{
	if ((MaxWeight-Weight) < 200 || checkweight(1201,1) == 0) {
		mes "- Espere um momento! -";
		mes "- Atualmente você está carregando -";
		mes "- muitos itens com você. -";
		mes "- Por favor volte novamente -";
		mes "- após guardar alguns de seus itens no Armazém Kafra. -";
		close;
	}
	mes "[Criador de Tinturas Java Dullihan]";
	mes "Uau...";
	mes "É um ótimo dia. Um dia perfeito para se criar Tinturas.";
	next;
	switch(select("Conversar:Criar Tinturas:Cancelar")) {
	case 1:
		mes "[Criador de Tinturas Java Dullihan]";
		mes "Erm, realmente não tenho muito a dizer para você. Mas, se você quiser que eu conte sobre minha vida, poderei fazer isso. É um pouco longa e chata, mas você gostaria de ouvir?";
		next;
		if (select("Ouvir:Não ouvir") == 1) {
			mes "[Criador de Tinturas Java Dullihan]";
			mes "Pelo que lembro, meu pai foi quem começou entre nós a fazer Tinturas.. Ele costumava passar inúmeras horas fazendo tinturas de cores diferentes. Até quando minha mãe faleceu ele não parou de fazê-las.";
			next;
			mes "[Criador de Tinturas Java Dullihan]";
			mes "Em um momento na minha vida, me tornei rebelde e fugi de casa.Eu não queria ser preso e prejudicar os negócios da família, mas também queria experimentar outras coisas.";
			next;
			mes "[Criador de Tinturas Java Dullihan]";
			mes "De qualquer forma...";
			mes "Eventualmente já fazem 15 anos que acabei voltando para minha casa 15 para continuar a tradição de família. Acho que isso realmente está no sangue.";
			next;
			mes "[Criador de Tinturas Java Dullihan]";
			mes "Percebi algo esses dias, agora eu posso entender meu pai. E o por quê do meu pai ter dedicado toda sua vida a fazer tinturas...";
			next;
			mes "[Criador de Tinturas Java Dullihan]";
			mes "No meio de todo desse difícil e tedioso trabalho, provavelmente ele sentiu a magia dessas cores que são passadas através dos sonhos para as pessoas.";
			next;
			mes "[Criador de Tinturas Java Dullihan]";
			mes "E foi provavelmente através desses sonhos que ele tentou arduamente fazer tinturas para seus clientes...";
			next;
			mes "[Criador de Tinturas Java Dullihan]";
			mes "Hehe... Como sei disso? Bem, essa é a maneira que percebo agora. A alguns anos atrás, comecei a ser capaz de ouvir o que as cores diziam...";
			next;
			mes "[Criador de Tinturas Java Dullian]";
			mes "Acredite. Um dia, eu irei conseguir fazer a cor que meu pai queria, mas nunca teve a chance de fazê-la.";
			close;
		}
		mes "[Criador de Tinturas Java Dullihan]";
		mes "Ahahahaha...";
		mes "Bem, já imaginava que ninguém quisesse ouvir minha misera história de Criador de Tinturas. Hahaha....";
		close;

	case 2:
		mes "[Criador de Tinturas Java Dullihan]";
		mes "Ótimo! Se você quiser a farei para você. Prometo que farei a cor que você quiser. Mas dependendo da cor a taxa em zenys é diferente.";
		next;
		mes "[Criador de Tinturas Java Dullihan]";
		mes "Por quê? Lembre-se de que existem cores que são fáceis de se fazer e aquelas que exigem mais esforço e trabalho. Por favor, não ache a taxa cara.";
		next;
		mes "[Criador de Tinturas Java Dullihan]";
		mes "A cor da tintura é especial, elas são feitas com todo meu coração e alma.";
		next;
		switch(select("Tintura Vermelha:Tintura Amarela:Tintura Azul:Tintura Verde:Tintura Laranja:Tintura Violeta:Tintura Branca:Tintura Preta:Cancelar")) {
		case 1: callsub S_MakeDye,1,1; break;
		case 2: callsub S_MakeDye,2,1; break;
		case 3: callsub S_MakeDye,3,2; break;
		case 4: callsub S_MakeDye,4,3; break;
		case 5: callsub S_MakeDye,5,4; break;
		case 6: callsub S_MakeDye,6,4; break;
		case 7: callsub S_MakeDye,7,4; break;
		case 8: callsub S_MakeDye,8,4; break;
		case 9:
			mes "[Criador de Tinturas Java Dullihan]";
			mes "Eeeehhhh!! O quê? Você muda de idéia agora!? É tão lamentável...";
			close;
		}

	case 3:
		mes "[Criador de Tinturas Java Dullihan]";
		mes "Não estou me vangloriando ou algo do tipo. Mas eu tenho as habilidades necessárias para se fazer tinturas. Se você precisar delas, por favor venha até mim e eu irei fazê-las para você.";
		close;
	}

S_MakeDye:
	mes "[Criador de Tinturas Java Dullihan]";
	switch(getarg(0)) {
	case 1:
		mes "Hum... Eu irei precisar de 30 Ervas Vermelha, 1 Antígeno, e 1 Garrafa Vazia para fazer a Tintura Vermelha. A taxa de criação são apenas 3000 zeny.";
		setarray .@item[0], 507, 973, 713;
		setarray .@count[0], 30,   1,   1;
		set .@cost,3000;
		set .@dyestuff,975;
		break;
	case 2:
		mes "Hum... Eu irei precisar de 30 Ervas Amarela, 1 Antígeno, e 1 Garrafa Vazia para fazer a Tintura Amarela. A taxa de criação são apenas 3000 zeny .";
		setarray .@item[0], 508, 973, 713;
		setarray .@count[0], 30,   1,   1;
		set .@cost,3000;
		set .@dyestuff,976;
		break;
	case 3:
		mes "Hum... Eu irei precisar de 20 Ervas Azul, 1 Antígeno, e 1 Garrafa Vazia para fazer a Tintura Azul. É muito difícil de se manipular Ervas Azuis, por isso a taaxa de criação será de 3500 zeny.";
		setarray .@item[0], 510, 973, 713;
		setarray .@count[0], 20,   1,   1;
		set .@cost,3500;
		set .@dyestuff,978;
		break;
	case 4:
		mes "Hum... Eu irei precisar de 5 Ervas Azuis, 20 Ervas Verde, 20 Ervas Amarela, 1 Antígeno, 1 Místura, e 1 Garrafa Vazia para criar uma Tintura Verde. Não confunda os materiais. A taxa de criação são apenas 5000 zeny.";
		setarray .@item[0], 510, 511, 508, 974, 973, 713;
		setarray .@count[0],  5,  20,  20,   1,   1,   1;
		set .@cost,5000;
		set .@dyestuff,979;
		break;
	case 5:
		mes "Hum... Eu irei precisar de 20 Ervas Vermelha, 20 Ervas Amarela, 1 Antígeno, 1 Místura, e 1 Garrafa Vazia para criar a Tintura Laranja. A taxa será de 5000 zeny.";
		setarray .@item[0], 507, 508, 974, 973, 713;
		setarray .@count[0], 20,  20,   1,   1,   1;
		set .@cost,5000;
		set .@dyestuff,980;
		break;
	case 6:
		mes "Hum... Eu irei precisar de 10 Ervas Azuis, 30 Ervas Vermelha, 1 Antígeno, 1 Místura, e 1 Garrafa Vazia para criar a Tintura Violeta. A taxa será de 5000 zeny.";
		setarray .@item[0], 510, 507, 974, 973, 713;
		setarray .@count[0], 10,  30,   1,   1,   1;
		set .@cost,5000;
		set .@dyestuff,981;
		break;
	case 7:
		mes "Hum... Eu irei precisar de 30 Ervas Brancas, 1 Antígeno, e 1 Garrafa Vazia para criar a Tintura Branca. A taxa será de 3000 zeny.";
		setarray .@item[0], 509, 973, 713;
		setarray .@count[0], 30,   1,   1; 
		set .@cost,3000;
		set .@dyestuff,982;
		break;
	case 8:
		mes "Hum... Eu irei precisar de 30 ervas de cada: Vermelha, Amarela, e Verde, 5 Ervas Azuis, 1 Antígeno, 1 Místura, e 1 Garrafa Vazia. O processo leva mais tempo e também mais esforço do que os outros, por isso a taxa será de 7000 zeny.";
		setarray .@item[0], 507, 508, 511, 510, 974, 973, 713;
		setarray .@count[0], 30,  30,  30,   5,   1,   1,   1;
		set .@cost,7000;
		set .@dyestuff,983;
		break;
	}
	next;
	mes "[Criador de Tinturas Java Dullihan]";
	switch(getarg(1)) {
	case 1: mes "Ah! Acredito que você já tenha tudo pronto. Gostaria de iniciar o processo?"; break;
	case 2: mes "Ok! Acredito que você já tenha tudo pronto. Deseja iniciar o processo?"; break;
	case 3: mes "Ok! Acredito que você já tenha tudo pronto. Devemos iniciar o processo?"; break;
	case 4: mes "Ok! Acredito que você já tenha tudo pronto. Gostaria de iniciar o processo?"; break;
	}
	next;
	if (select("Criar Tinturas:Cancelar") == 1) {
		set .@size, getarraysize(.@item);
		for( set .@i,0 ; .@i < .@size ; set .@i,.@i+1 )
			if( countitem(.@item[.@i]) < .@count[.@i] )
				break;
		if( .@i < .@size || Zeny < .@cost ) {
			mes "[Criador de Tinturas Java Dullihan]";
			mes "Humm. Não é o suficiente...";
			mes "Não acho que serei capaz de fazer a cor que você deseja apenas com esses materiais. Por que você não consegue mais alguns materiais...?";
			close;
		}
		for (set .@i, 0; .@i < .@size ; set .@i, .@i+1)
			delitem .@item[.@i], .@count[.@i];
		set zeny,zeny-.@cost;
		getitem .@dyestuff,1;
		
		mes "[Criador de Tinturas Java Dullihan]";
		mes "Hum... Ela ficou muito boa. Uma cor muito rica. E é claro que é mais difícil de se fazer uma cor mais charmosa como essa...";
		next;
		mes "[Criador de Tinturas Java Dullihan]";
		mes "Bem, então. Venha sempre aqui caso precise de mais tinturas.";
		close;
	}
	mes "[Criador de Tinturas Java Dullihan]";
	mes "Eeeehhhh!! O quê??";
	mes "Você muda de idéia agora!? É tão lamentável...";
	return;
}