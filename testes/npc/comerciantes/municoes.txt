//    ________________________________________________________
//   /                                                        \
//   |       _           _   _   _                            |
//   |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
//   |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
//   |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
//   |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
//   |                                                        |
//   |                    brAthena Script                     |
//   |--------------------------------------------------------|
//   | Nome do Script: municoes.txt                           |
//   |--------------------------------------------------------|
//   | Criado por: eA/rA/brA Dev. Team                        |
//   |--------------------------------------------------------|
//   | Versões eA/rA/brA: 1.6/1.2/1.1/1.0                     |
//   |--------------------------------------------------------|
//   | Descrição: Negociador de munições.                     |
//   |--------------------------------------------------------|
//   | Changelog:                                             |
//   | 1.0 Criado Cabeçalho [Shiraz]                          |
//   | - Anotações                                            |
//   \________________________________________________________/

alberta,118,160,3	script	Bullet Dealer Tony#alb::bdt	86,{

	if (BaseJob == Job_Gunslinger) {
		mes "[Tony]";
		mes "I'm Tony, the Bullet Dealer.";
		mes "Come to me whenever you're";
		mes "short on ammo. Just bring me";
		mes "the materials, and I'll make";
		mes "you the bullets you need.";
		next;
		switch(select("Poison Sphere:Flare Sphere:Lighting Sphere:Blind Sphere:Freezing Sphere:Cancel")) {
		case 1: callfunc "Bullet_Trade",937,10,13205; break;
		case 2: callfunc "Bullet_Trade",7097,2,13203; break;
		case 3: callfunc "Bullet_Trade",7053,3,13204; break;
		case 4: callfunc "Bullet_Trade",1024,5,13206; break;
		case 5: callfunc "Bullet_Trade",7054,2,13207; break;
		case 6:
			mes "[Tony]";
			mes "Changed your mind?";
			mes "Well, if you ever need";
			mes "any bullets, I'll be right";
			mes "here. Come back whenever";
			mes "you think you'll need more";
			mes "ammunition, Gunslinger.";
			close;
		}
	}
	mes "[Tony]";
	mes "Hey, I'm Tony. I'm in";
	mes "charge of distributing";
	mes "and making bullets for";
	mes "Gunslingers. It's just";
	mes "how our guild likes";
	mes "to do things.";
	next;
	mes "[Tony]";
	mes "I'm sorry if you came";
	mes "here to buy some bullets.";
	mes "I can only do business with";
	mes "fully fledged Gunslingers.";
	close;
}

function	script	Bullet_Trade	{
	mes "[Tony]";
	mes "For every set of";
	mes "30 "+getitemname(getarg(2))+"s,";
	mes "you must give me";
	mes "1 Emveretarcon,";
	mes "1 Phracon, and";
	mes ""+getarg(1)+" "+getitemname(getarg(0))+".";
	next;
	mes "[Tony]";
	mes "Remember that I can give";
	mes "a maximum of 500 sets of";
	mes "30 bullets at a time. Please";
	mes "enter the number of bullet sets";
	mes "that you'd like. If you want to";
	mes "cancel, then just enter ''0.''";
	next;
	input .@amount;
	if (.@amount < 1 || .@amount > 500) {
		mes "[Tony]";
		mes "Hey, I can't give you";
		mes "that many bullets. Don't";
		mes "forget to enter a number";
		mes "that's no higher than 500";
		mes "if you want to trade your";
		mes "items for some bullets.";
		close;
	}
	if (countitem(1010) >= .@amount && countitem(1011) >= .@amount && countitem(getarg(0)) >= (.@amount*getarg(1))) {
		if (checkweight(getarg(2),.@amount * 30) == 0) {
			mes "[Tony]";
			mes "Eh? Your Inventory doesn't";
			mes "have enough space for this";
			mes "many bullets. Come back later";
			mes "after you make more space";
			mes "available. Try putting some of";
			mes "your things into Kafra Storage.";
			close;
		}else{
			mes "[Tony]";
			mes "Great, everything seems";
			mes "to be in order. Let me take";
			mes "these materials, and here are";
			mes "your bullets. It's a pleasure";
			mes "to do business with you~";
			delitem 1010,.@amount;
			delitem 1011,.@amount;
			delitem getarg(0),.@amount * getarg(1);
			getitem getarg(2),.@amount * 30;
			close;
		}
	}else{
		mes "[Tony]";
		mes "Huh. It looks like you";
		mes "don't have enough materials";
		mes "for that many bullets. Well,";
		mes "it's no problem. Just come";
		mes "back after gathering everything";
		mes "that you need, okay?";
		close;
	}
}

// Caixa de Munições

alberta,118,157,3	script	Magazine Dealer Tonny::mdk	83,{
	set .n$, compare(strnpcinfo(1),"Tonny")?"[Tonny]":"[Kenny]";
	if(BaseJob == Job_Gunslinger) {
		mes .n$;
		mes "Welcome to my Magazine Shop.";
		mes "As you may know, large numbers";
		mes "of bullets can be carried more";
		mes "easily when they're in Magazines. Now, can I interest you in";
		mes "anything in particular?";
		next;
		switch(select("Wind Sphere Pack:Shadow Sphere Pack:Poison Sphere Pack:Water Sphere Pack:Fire Sphere Pack:Cartridge:Blood Cartridge:Silver Cartridge:Cancel")) {
		case 1: callfunc "Func_Casing",13204,12144; break;
		case 2: callfunc "Func_Casing",13206,12145; break;
		case 3: callfunc "Func_Casing",13205,12146; break;
		case 4: callfunc "Func_Casing",13207,12147; break;
		case 5: callfunc "Func_Casing",13203,12148; break;
		case 6: callfunc "Func_Casing",13200,12149; break;
		case 7: callfunc "Func_Casing",13202,12150; break;
		case 8: callfunc "Func_Casing",13201,12151; break;
		case 9:
			mes .n$;
			mes "Well, if you ever find";
			mes "that you have too many";
			mes "bullets, come and see me.";
			mes "It's a smart idea to store";
			mes "bullets with my Magazines.";
			close;
		}
	}
	mes .n$;
	mes "Welcome to my shop.";
	mes "Here, I provide Magazines";
	mes "and Cartridges for Gunslingers.";
	mes "Sorry, but it doesn't look like";
	mes "my services would be of any";
	mes "use to you, adventurer.";
	next;
	mes .n$;
	mes "Eh, but if you happen to";
	mes "know any Gunslingers, send";
	mes "them my way. You can never";
	mes "have too many bullets.";
	close;
}

function	script	Func_Casing	{
	mes .n$;
	mes "Now, you can trade";
	mes "500 "+getitemname(getarg(0))+"s";
	mes "and 500 zeny for 1 "+getitemname(getarg(1))+", so make sure";
	mes "you have sufficient bullets";
	mes "and zeny for this exchange.";
	next;
	mes .n$;
	mes "You can exchange 500 "+getitemname(getarg(0))+"s and 500 zeny";
	mes "with 1 "+getitemname(getarg(1))+".";
	next;
	mes .n$;
	mes "Remember that I can't give";
	mes "you more than 50 Magazines";
	mes "at a time. Now please enter";
	mes "the number of Magazines you";
	mes "want to receive. If you want to cancel, then just enter ''0.''";
	next;
	input .@amount;
	if(.@amount > 50 || .@amount == 0) {
		mes .n$;
		mes "Hey, I can't give you";
		mes "that many Magazines.";
		mes "Please try again, and";
		mes "enter a number no";
		mes "greater than 50.";
		close;
	}
	if (countitem(getarg(0)) >= (500*.@amount)) {
		if (Zeny >= (500*.@amount)) {
			if (checkweight(getarg(1),.@amount) == 0) {
				mes .n$;
				mes "Hey, you've got a lot";
				mes "of junk crammed in your";
				mes "Inventory. Free up some";
				mes "space, and then come back";
				mes "and trade your bullets for";
				mes "some Magazines later, okay?";
				close;
			}else{
				mes .n$;
				mes "Alright, here are";
				mes "your Magazines. Thanks";
				mes "for visiting my shop, and";
				mes "I hope that you use all";
				mes "of your ammo wisely.";
				set Zeny,Zeny-500*.@amount;
				delitem getarg(0),500*.@amount;
				getitem getarg(1),.@amount;
				close;
			}
		}else{
			mes .n$;
			mes "Sorry, but you don't";
			mes "have enough zeny for";
			mes "this Magazine exchange.";
			mes "Come back to my shop";
			mes "after you've saved up";
			mes "some more money.";
			close;
		}
	}else{
		mes .n$;
		mes "Sorry, but you don't";
		mes "have enough bullets for";
		mes "this Magazine exchange.";
		mes "Maybe you should double";
		mes "check your Inventory, and";
		mes "then come back to me later.";
		close;
	}
}

izlude,158,130,3	duplicate(bdt)	Bullet Dealer Tony#iz	86
izlude,155,132,3	duplicate(mdk)	Magazine Dealer Tony#iz	83