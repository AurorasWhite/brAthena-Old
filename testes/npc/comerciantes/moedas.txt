//    ________________________________________________________
//   /                                                        \
//   |       _           _   _   _                            |
//   |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
//   |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
//   |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
//   |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
//   |                                                        |
//   |                    brAthena Script                     |
//   |--------------------------------------------------------|
//   | Nome do Script: Mercadores de Manuk e Esplendido       |
//   |--------------------------------------------------------|
//   | Criado por: eA/rA/brA Dev. Team                        |
//   |--------------------------------------------------------|
//   | Versões eA/rA/brA: 1.2/1.1/1.2                         |
//   |--------------------------------------------------------|
//   | Descrição: NPCs de resgate da moeda.                   |
//   |--------------------------------------------------------|
//   | Changelog:                                             |
//   | 1.0 Criado Cabeçalho [Shiraz]                          |
//   | 1.1 Tradução PT-BR [insidemybrain]                     |
//   | 1.2 Otimização [Mark]                                  |
//   |--------------------------------------------------------|
//   | - Anotações                                            |
//   \________________________________________________________/

man_in01,286,16,1	script	Mercador de Manuk	439,{
	set .@n$, "["+strnpcinfo(1)+"]";
	mes .@n$;
	if (isequipped(2782) && ep13_2_rhea == 100) {
		if (!checkweight(1201,1)) {
			mes "Parece que você está carregando itens demais.";
			mes "Por que não os põe no Armazém e depois retorna?";
			close;
		}
		mes "Olá.";
		mes "O que você está procurando?";
		next;
		if (strnpcinfo(1) == "Mercador de Manuk") {
			setarray .@Items[2],12342,12343,12348;
			set .@Livro,11019; // Itens de Esplendor à Venda
			set .@Moeda,6080;  // Moeda Manuk
			set .@i, select("Ver a descrição dos itens:Oportunidade de Manuk:Coragem de Manuk:Fé de Manuk:Cancelar");
		} else {
			setarray .@Items[2],12344,12345,12349;
			set .@Livro,11018; // Itens de Esplendor à Venda
			set .@Moeda,6081;  // Moeda Esplendor
			set .@i, select("Ver a descrição dos itens:Comprar Geleia de Fruta de Pinguicula:Comprar Geleia de Mel de Vagalume:Lágrimas de Cornus:Nada");
		}
		mes .@n$;
		if (.@i == 1) {
			mes "Aqui está a descrição dos itens.";
			close2;
			ReadBook .@Livro,1;
			end;
		} else if (.@i < 5) {
			mes "Eu posso lhe vender 3 "+getitemname(.@Items[.@i])+" por ^3131FF1 moedas^000000.";
			next;
			switch(select("Comprar:Não comprar")) {
			case 1:
				if (countitem(.@Moeda)) {
					mes .@n$;
					mes "Obrigado pelo bom negócio.";
					delitem .@Moeda,1; 
					getitem .@Items[.@i],3;
					close;
				} else {
					mes .@n$;
					mes "Você não tem moedas o suficente.";
					close;
				}
			case 2:
				mes .@n$;
				break;
			}
		}
		mes "Volte caso mude de ideia.";
		close;
	} else {
		if (strnpcinfo(1) == "Mercador de Manuk") {
			mes "Rtt od d";
			mes "Qwo hd is d irr";
		}
		else {
			mes "BurWehAla";
			mes "tasnarAndu Ie Ru";
		}
		close;
	}
}
spl_in01,110,326,5	duplicate(Mercador de Manuk)	Mercador de Esplendido	439