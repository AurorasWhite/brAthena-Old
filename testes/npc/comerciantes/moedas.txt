//    ________________________________________________________
//   /                                                        \
//   |       _           _   _   _                            |
//   |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
//   |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
//   |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
//   |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
//   |                                                        |
//   |                    brAthena Script                     |
//   |--------------------------------------------------------|
//   | Nome do Script: moedas.txt                             |
//   |--------------------------------------------------------|
//   | Criado por: eA/rA/brA Dev. Team                        |
//   |--------------------------------------------------------|
//   | Versões eA/rA/brA: 1.2/1.1/1.0                         |
//   |--------------------------------------------------------|
//   | Descrição: NPCs de resgate da moeda.                   |
//   |--------------------------------------------------------|
//   | Changelog:                                             |
//   | 1.0 Criado Cabeçalho [Shiraz]                          |
//   | - Anotações                                            |
//   \________________________________________________________/

-	script	::merchant_13_2	-1,{
	set .@n$, "["+strnpcinfo(1)+"]";
	mes .@n$;
	if (isequipped(2782) && ep13_2_rhea == 100) {
		if (!checkweight(1201,1)) {
			mes "It looks like you're carrying too many things.";
			mes "Why not put some of your items in storage and come back?";
			close;
		}
		mes "Hello.";
		mes "What are you looking for?";
		next;
		if (strnpcinfo(1) == "Merchant of Manuk") {
			setarray .@Items[2],12342,12343,12348;
			set .@Book,11019; //Manuk_Selling_Item
			set .@Coin,6080; //Manuk_Coin
			set .@i, select("View item description:Manuk's Opportunity:Manuk's Courage:Manuk's Faith:Cancel");
		}
		else {
			setarray .@Items[2],12344,12345,12349;
			set .@Book,11018; //Splendide_Selling_Item
			set .@Coin,6081; //Splendide_Coin
			set .@i, select("View item description:Buy Pinguicula's Fruit Jam:Buy Luciola's Honey Jam:Buy Cornus' Tears:Do nothing");
		}
		mes .@n$;
		if (.@i == 1) {
			mes "Here are the item descriptions.";
			close2;
			ReadBook .@Book,1;
			end;
		}
		else if (.@i < 5) {
			mes "I can sell you 3 "+getitemname(.@Items[.@i])+" for ^3131FF1 coin^000000.";
			next;
			switch(select("Purchase:Do not purchase")) {
			case 1:
				if (countitem(.@Coin)) {
					mes .@n$;
					mes "Thank you for coming.";
					delitem .@Coin,1; 
					getitem .@Items[.@i],3;
					close;
				}
				else {
					mes .@n$;
					mes "You don't have enough coins.";
					close;
				}
			case 2:
				mes .@n$;
				break;
			}
		}
		mes "Come again if you change your mind.";
		close;
	}
	else {
		if (strnpcinfo(1) == "Merchant of Manuk") {
			mes "Rtt od d";
			mes "Qwo hd is d irr";
		}
		else {
			mes "BurWehAla";
			mes "tasnarAndu Ie Ru";
		}
		close;
	}
}
man_in01,286,16,1	duplicate(merchant_13_2)	Merchant of Manuk	454
spl_in01,110,326,5	duplicate(merchant_13_2)	Merchant of Splendide	439