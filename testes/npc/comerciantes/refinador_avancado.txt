//    ________________________________________________________
//   /                                                        \
//   |       _           _   _   _                            |
//   |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
//   |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
//   |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
//   |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
//   |                                                        |
//   |                    brAthena Script                     |
//   |--------------------------------------------------------|
//   | Nome do Script: refinador_avancado.txt                 |
//   |--------------------------------------------------------|
//   | Criado por: eA/rA/brA Dev. Team                        |
//   |--------------------------------------------------------|
//   | Versões eA/rA/brA:   ??/1.0/1.0                        |
//   |--------------------------------------------------------|
//   | Descrição: Refinador Avançado de malangdo.             |
//   |--------------------------------------------------------|
//   | Changelog:                                             |
//   | 1.0 Criado Cabeçalho [Megasantos]                      |
//   |--------------------------------------------------------|
//   | - Anotações                                            |
//   \________________________________________________________/

payon,157,146,6	script	Suhnbi#cash	85,{
	disable_items;
	mes "[Suhnbi]";
	mes "I am the Armsmith";
	mes "I can refine all kinds of weapons,";
	mes "armor and equipment, so let me";
	mes "know what you want to refine.";
	next;
	setarray .@position$[1], "Head","Body","Left hand","Right hand","Robe","Shoes","Accessory 1","Accessory 2","Head 2","Head 3";
	set .@menu$,"";
	for( set .@i,1; .@i <= 10; set .@i,.@i+1 )
	{
		if( getequipisequiped(.@i) )
			set .@menu$, .@menu$ + .@position$[.@i] + "-" + "[" + getequipname(.@i) + "]";

		set .@menu$, .@menu$ + ":";
	}
	set .@part,select(.@menu$);

	if( !getequipisequiped(.@part) )
		close;

	if( !getequipisenableref(.@part) )
	{
		mes "[Suhnbi]";
		mes "I don't think I can refine this item at all.";
		close;
	}
	if( getequiprefinerycnt(.@part) >= 10 )
	{
		mes "[Suhnbi]";
		mes "This item cannot be refined because it has already reached its maximum level...";
		close;
	}

	// Make sure you have the neccessary items and Zeny to refine your items
	// Determines chance of failure and verifies that you want to continue.
	switch( getequipweaponlv(.@part) )
	{
		case 1: callsub S_RefineValidate,1,7620,50,.@part; break;
		case 2: callsub S_RefineValidate,2,7620,200,.@part; break;
		case 3: callsub S_RefineValidate,3,7620,5000,.@part; break;
		case 4: callsub S_RefineValidate,4,7620,20000,.@part; break;
		default: callsub S_RefineValidate,0,7619,2000,.@part; break;
	}

	if( getequippercentrefinery(.@part) > rand(100) || getequippercentrefinery(.@part) > rand(100) )
	{
		mes "[Suhnbi]";
		mes "Clink! Clank! Clunk!";
		SuccessRefItem .@part;
		next;
		Emotion e_no1;
		mes "[Suhnbi]";
		mes "Here you are! It's done.";
		mes "It's been a while since I've made such a fine weapon. You must be happy because it has become stronger!";
		close;
	}
	mes "[Suhnbi]";
	mes "Clink! Clank! Clunk!";
	FailedRefItem .@part;
	next;
	if (rand(5) == 1){
		Emotion e_cash;
	} else {
		Emotion e_omg;
	}
	mes "[Suhnbi]";
	mes "Cough!!!!";
	next;
	mes "[Suhnbi]";
	mes "Cough...Cough..";
	mes "What a shame...";
	mes "Your equipment broke during the refining process. I had told you earlier this might happen!";
	close;

S_RefineValidate:
	mes "[Suhnbi]";
	if (getarg(0))
		mes "A level " + getarg(0) + " weapon...";
	mes "To refine this I need one ^ff9999" + getitemname(getarg(1)) + "^000000 and a service fee of " + getarg(2) + " Zeny.";
	mes "Do you wish to continue?";
	next;
	if( select("Yes:No") == 1 )
	{
		if( getequippercentrefinery(getarg(3)) < 100 )
		{
			mes "[Suhnbi]";
			mes "Wow!!";
			mes "This weapon, probably";
			mes "looks like it's been refined...";
			mes "many times...";
			mes "It may break if";
			mes "you refine it again.";
			next;
			mes "And if it breaks,";
			mes "you can't use it anymore!";
			mes "All the cards in it and the";
			mes "properties";
			mes "^ff0000will be lost^000000!!";
			mes "^ff0000 besides, the equipment will break!^000000";
			mes " ";
			mes "Are you sure you still want to continue?";
			next;
			if( select("Yes:No") == 2 )
			{
				mes "[Suhnbi]";
				mes "I completely agree...";
				mes "I might be a great refiner, but sometimes even I make mistakes.";
				close;
			}
		}
		if( countitem(getarg(1)) > 0 && Zeny > getarg(2) )
		{
			delitem getarg(1), 1;
			set Zeny, Zeny - getarg(2);
			return;
		}
		mes "[Suhnbi]";
		mes "You don't seem to have enough Zeny or " + getitemname(getarg(1)) + "...";
		mes "Go get some more. I'll be here all day if you need me.";
		close;
	}
	mes "[Suhnbi]";
	mes "Yeah... There's no need to rush.";
	mes "Take your time.";
	close;
}

malangdo,221,174,6	script	Holink#mal_cash	559,{
	disable_items;
	mes "[Holink]";
	mes "I am the meow~ Blacksmith Holink~";
	mes "Master of refining, Holink~";
	mes "I am the special cat Holink who learned from Morroc~";
	mes "My daughter is so proud of me, Holink~";
	mes "What should Holink~ refine today?";
	next;
	setarray .@position$[1],"Head","Body","Left Hand","Right Hand","Robe","Shoes","Accessory 1","Accessory 2","Head 2","Head 3";
	set .@menu$,"";
	for(set .@i,1; .@i<=10; set .@i,.@i+1)
		set .@menu$, .@menu$+((getequipisequiped(.@i))?getequipname(.@i):.@position$[.@i]+"-[Empty]")+":";
	set .@part, select(.@menu$);
	if (!getequipisequiped(.@part)) {
		mes "[Holink]";
		switch(.@part) {
		case 1:
			mes "My teacher Aragam said meow~";
			mes "There's no cure for stupidity...";
			break;
		case 2:
			mes "There's nothing to see here, meow!!";
			break;
		case 3:
			mes "Meow? What do you want me to do with this left hand...?";
			break;
		case 4:
			mes "Meow? What do you want me to do with this right hand...?";
			break;
		case 5:
			mes "Meow? You don't have anything on.";
			break;
		case 6:
			mes "Kyang~! Do not mess with my sensitive olfactory, meow~.";
			break;
		case 7:
		case 8:
			mes "Meow? Where is the accessory?";
			break;
		case 9:
		case 10:
			mes "Meow? You talking about the other head parts, meow?~";
			break;
		}
		close;
	}
	if (!getequipisenableref(.@part)) {
		mes "[Holink]";
		mes "Even Aragam can't refine such a thing, meow.";
		close;
	}
	if (!getequipisidentify(.@part)) {
		mes "[Holink]";
		mes "Meow? How did you wear something that wasn't appraised, meow?~";
		close;
	}
	if (getequiprefinerycnt(.@part) >= 10) {
		mes "[Holink]";
		mes "Meow~ Perfect refining. Did Aragam do this, meow?~";
		close;
	}
	mes "[Holink]";
	switch(getequipweaponlv(.@part)) {
	default:
	case 0: // Armor
		set .@price,15000;
		set .@material,7619; //Enriched_Elunium
		set .@type$,"armor";
		mes "You have chosen an armor, meow~";
		break;
	case 1: // Level 1 Weapon
		set .@price,500;
		set .@material,7620; //Enriched_Oridecon
		set .@type$,"weapon";
		mes "A level 1 weapon...?";
		break;
	case 2: // Level 2 Weapon
		set .@price,2000;
		set .@material,7620; //Enriched_Oridecon
		set .@type$,"weapon";
		mes "Meow, a level 2 weapon...?";
		break;
	case 3: // Level 3 Weapon
		set .@price,20000;
		set .@material,7620; //Enriched_Oridecon
		set .@type$,"weapon";
		mes "Meow Meow~~ A level 3 weapon~~";
		break;
	case 4: // Level 4 Weapon
		set .@price,50000;
		set .@material,7620; //Enriched_Oridecon
		set .@type$,"weapon";
		mes "Me-Meow!... A level 4 weapon...!";
		mes "I've only seen it twice while";
		mes "learning from Aragam... Me-Meow!!";
		break;
	}
	mes "You need ^ff9999"+getitemname(.@material)+"^000000 and ^ff9999"+.@price+"^000000 Zeny for this refine, meow~";
	mes "Want to continue, meow?~";
	next;
	if(select("Yes!!:No!!") == 2) {
		mes "[Holink]";
		mes "Kyack!!";
		mes "You don't belive in refine master Holink, meow?~";
		close;
	}
	if (getequippercentrefinery(.@part) < 100) {
		mes "[Holink]";
		mes "Meow!!";
		if (.@type$ == "armor")
			mes "This armor was already refined so many times, meow.";
		else {
			mes "Danger. Danger~";
			mes "This weapon was refined a lot, meow~";
			next;
			mes "[Holink]";
		}
		mes "You might break it if you continue";
		mes "to try refining this item further, meow.";
		next;
		mes "[Holink]";
		mes "Once the "+.@type$+" is broken, you can";
		mes "never use it again, meow. Not to mention... all current";
		mes "^ff0000cards and enchantments will vanish for sure^000000.";
		mes "You still want to try, meow~?";
		next;
		if(select("Yes, I do!!:Forget about it!!") == 2) {
			mes "[Holink]";
			mes "Meow! Wise choice, meow.";
			mes "But!!";
			mes "I am not happy to see you doubting the refine master Holink, meow~";
			close;
		}
	}
	if (countitem(.@material) == 0 || Zeny < .@price) {
		mes "[Holink]";
		mes "You don't have the ingredients.";
		mes "You need ^ff9999"+getitemname(.@material)+"^000000 and ^ff9999"+.@price+"^000000 Zeny, meow~";
		mes "Go get it, meow~";
		close;
	}
	delitem .@material,1;
	set Zeny, Zeny-.@price;
	if (getequippercentrefinery(.@part) > rand(100) || getequippercentrefinery(.@part) > rand(100)) {
		successrefitem .@part;
		mes "[Holink]";
		mes "Me~ Me~ Meow! Fun fun refining~";
		next;
		emotion e_kis;
		mes "[Holink]";
		mes "Perfect!! Perfect, meow!!";
		mes "I am the disciple of the refining wizard Aragam~";
		mes "Holink!!";
		mes "Another day of successful refining, meow!!";
		close;
	}
	failedrefitem .@part;
	mes "[Holink]";
	mes "Meo~ Meow~ Kyaaak!!";
	next;
	switch(rand(1,5)) {
		case 1: emotion e_sob; break;
		case 2: emotion e_swt2; break;
		case 3: emotion e_wah; break;
		case 4: emotion e_hmm; break;
		case 5: emotion e_lv2; break;
	}
	mes "[Holink]";
	mes "Meow!! Aaaaakk~~!!!!";
	mes "Kyaaak!! I have failed, meow!!";
	next;
	mes "[Holink]";
	mes "......";
	mes "......";
	mes "All~ Everything~ Broken, meow...";
	next;
	mes "[Holink]";
	mes "Meow.... Master Aragam once said,";
	mes "learn from your failures...";
	mes "Human, this one failure will be the beginning of your success in the future.";
	close;
}
