//    ________________________________________________________ 
//   /                                                        \
//   |       _           _   _   _                            |
//   |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
//   |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
//   |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
//   |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
//   |                                                        |
//   |                    brAthena Script                     |
//   |--------------------------------------------------------|
//   | Nome do Script: novico.txt                             |
//   |--------------------------------------------------------|
//   | Criado por: eA/rA/brA Dev. Team                        |
//   |--------------------------------------------------------|
//   | Versões eA/rA/brA: 2.2/1.1/1.1                         |
//   |--------------------------------------------------------|
//   | Descrição: Mudança de Classe para Noviço.              |
//   |--------------------------------------------------------|
//   | Changelog:                                             |
//   | 1.0 Criado Cabeçalho [Megasantos]                      |
//   | 1.1 Tradução PT-BR [Holy]                              |
//   |--------------------------------------------------------|
//   | - Anotações                                            |
//   \________________________________________________________/

prt_church,184,41,4	script	Bispo Tomas Cerbantes#nov	60,{
	if (Upper == 1) {
		if (ADVJOB == Job_High_Priest || ADVJOB == Job_Champion) {
			if (Class == Job_Novice_High) {
				mes "[Tomas Cerbantes]";
				mes "Ah, eu sinto seu vigor";
				mes "e experiência. Você deve ter aprendido muitas coisas antes de entrar em Valhalla.";
				next;
				if (getskilllv("NV_BASIC") < 9) {
					mes "[Tomas Cerbantes]";
					mes "Infelizmente, eu acho que você ainda não está pronto para se tornar um Noviço. Por favor, conclua o aprendizado de todas as Competências Básicas primeiro.";
					next;
					mes "[Tomas Cerbantes]";
					mes "Entretanto,";
					mes "vou esperar até";
					mes "que você esteja pronto.";
					mes "Que Deus esteja";
					mes "com você.";
					close;
				}
				mes "[Tomas Cerbantes]";
				mes "Bem, eu recebê-o";
				mes "ao voltar de Valhalla e";
				mes "desejo-lhe sorte na sua";
				mes "nova jornada.";
				next;
				skill 143,0,0;
				jobchange Job_Acolyte_High;
				skill 156,1,0;
				mes "[Tomas Cerbantes]";
				mes "Agora, aventure-se e procure aqueles que necessitam de sua ajuda. Que Deus ilumine seu caminho.";
				close;
			}else{
				mes "[Tomas Cerbantes]";
				mes "Agora, aventure-se e procure aqueles que necessitam de sua ajuda. Que Deus ilumine vocês.";
				close;
			}
		}else{
			mes "[Tomas Cerbantes]";
			mes "Sinto que você passou por uma experiência de vida passada. Você deve ter aprendido muitas coisas antes de entrar em Valhalla.";
			next;
			mes "[Tomas Cerbantes]";
			mes "No entanto, posso dizer que você não está preparado para ser um Noviço. Lembre-se de quem você era em sua vida passada e encontre o seu caminho.";
			close;
		}
	}
	mes "[Tomas Cerbantes]";
	mes "O que é que você procura?";
	next;
	if ( !checkre(0) ) {
		switch(select("Father, I want to be a Acolyte.:Acolyte Requirements.:Just looking around.")) {
	case 1:
		mes "[Father Mareusis]";
		if (BaseJob == Job_Acolyte) {
			mes "Are you feeling okay today? I can tell by your attire that you are already an Acolyte. You're not joking around, are you?";
			close;
		}
		else if (BaseJob != Job_Novice) {
			mes "I'm sorry, but we can only accept Novices as applicants for the job change to Acolyte.";
			close;
		}
		if (job_acolyte_q == 0) {
			mes "Do you truly";
			mes "wish to become";
			mes "a servant of God?";
			next;
			if (select("Yes Father, I do.:Nope, I lied.") == 1) {
				mes "[Father Mareusis]";
				mes "Good. I accept " + strcharinfo(0) + "'s will to become an Acolyte. You understand that you must do penance before you can become a servant of God, right?";
				next;
				mes "[Father Mareusis]";
				mes "Well, I will";
				mes "give you a mission...";
				switch(rand(3)) {
				default:
					set job_acolyte_q,2;
					mes "Please visit ^000077Father Rubalkabara^000000, a member of the Prontera Parish, and return here. He has been practicing asceticism in the ^000077Relics NorthEast of Prontera City^000000.";
					setquest 1001;
					break;
				case 1:
					set job_acolyte_q,3;
					mes "Please visit ^000077Mother Mathilda^000000 and then return to me. She has been practicing asceticism near ^000077Morroc Town, SouthWest of Prontera City^000000.";
					setquest 1002;
					break;
				case 2:
					set job_acolyte_q,4;
					mes "Please visit ^000077Father Yosuke^000000 and return here. He has been practicing asceticism around ^000077a bridge somewhere NorthWest of Prontera^000000.";
					setquest 1003;
					break;
				}
				next;
				mes "[Father Mareusis]";
				mes "May the grace of God light your path and guide you during your journey of penance.";
				close;
			}
			mes "[Father Mareusis]";
			mes "You lied?";
			mes "It is good that you";
			mes "have confessed your";
			mes "wrongdoing. Go in";
			mes "peace, my son.";
			close;
		}
		mes "Oh, you've come back. Let me check and see if you are ready to serve God. Let's see...";
		next;
		mes "[Father Mareusis]";
		if (getskilllv("NV_BASIC") < 9) {
			mes "Good Lord! Haven't you accomplished the Basic Training yet?! It's important that you finish that!";
			next;
			mes "[Father Mareusis]";
			mes "You should have trained more! Go back and make sure you reach Novice Job Level 9 and learn all of the Basic Skills!";
			close;
		}
		if (job_acolyte_q < 5) {
			mes "Oh? I can't find your name on the Registration List.";
			next;
			switch(job_acolyte_q) {
			case 2:
				mes "[Father Mareusis]";
				mes "Please visit ^000077Father Rubalkabara^000000, a member of the Prontera Parish, and return here.";
				next;
				mes "[Father Mareusis]";
				mes "He has been practicing asceticism in the ^000077Relics at the NorthEast of Prontera City^000000.";
				break;
			case 3:
				mes "[Father Mareusis]";
				mes "Please Visit ^000077Mother Mathilda^000000 and return here to me.";
				next;
				mes "[Father Mareusis]";
				mes "She has been practicing asceticism near ^000077Morroc Town, located SouthWest of Prontera City^000000.";
				break;
			case 4:
				mes "[Father Mareusis]";
				mes "Please visit ^000077 Father Yosuke ^000000 and return here to me.";
				next;
				mes "[Father Mareusis]";
				mes "He has been practicing asceticism near a ^000077bridge somewhere to the NorthWest of Prontera^000000.";
				break;
			}
			next;
			mes "[Father Mareusis]";
			mes "May the grace of God brighten your path and guide you on your journey of penance.";
			close;
		}
		mes "Hmm...";
		mes "Your name is on the list and you've proven your qualification.";
		next;
		mes "[Father Mareusis]";
		mes "I am proud to say that you are now ready to become an Acolyte!";
		next;
		skill 143,0,0;
		callfunc "Job_Change",Job_Acolyte;
		callfunc "F_ClearJobVar";
		if(checkquest(1001) != -1) {
			completequest 1001;
		}
		else if(checkquest(1002) != -1) {
			completequest 1002;
		}
		else {
			completequest 1003;
		}
		mes "[Father Mareusis]";
		mes "Always remember to be thankful to God, who is taking care of us all the time.";
		next;
		mes "[Father Mareusis]";
		mes "Always use your gifts to serve Him by helping others. In chaos and times of difficulty, face your hardships with unwavering faith.";
		next;
		mes "[Father Mareusis]";
		mes "Lastly, I want to sincerely congratulate you on persevering through your trial of penance.";
		close;
	case 2:
		mes "[Father Mareusis]";
		mes "Do you wish to become an Acolyte? You must fulfill two requirements.";
		next;
		mes "[Father Mareusis]";
		mes "First, you have to reach at least Novice Job Level 9 and learn all of the Basic Skills. Second, you will be given a trial of penance to overcome.";
		next;
		mes "[Father Mareusis]";
		if (job_acolyte_q != 0) {
			switch(job_acolyte_q) {
			case 2:
				mes "For your trial, please visit ^000077Father Rubalkabara ^000000 and then return here to me.";
				next;
				mes "[Father Mareusis]";
				mes "He is practicing asceticism in the ^000077Relics at the NorthEast of Prontera City^000000.";
				break;
			case 3:
				mes "For your trial, please visit ^000077Mother Mathilda^000000 and return here to me.";
				next;
				mes "[Father Mareusis]";
				mes "She has been practicing asceticism near ^000077Morroc, located to the SouthWest of Prontera City^000000.";
				break;
			default:
				mes "For your trial, please visit ^000077Father Yosuke^000000 and return here to me.";
				next;
				mes "[Father Mareusis]";
				mes "He has been practicing asceticism around a bridge somewhere ^000077NorthWest of Prontera^000000.";
				break;
			}
			next;
			mes "[Father Mareusis]";
			mes "May the grace of God light your path and guide you on your journey of penance.";
		}
		else {
			mes "The destination for this trial will be decided once you fill the application form.";
		}
		next;
		mes "[Father Mareusis]";
		mes "Please come back after fulfilling the two requirements I've asked of you. As long as your desire to serve God and others is sincere, you will be able to make it.";
		close;
	case 3:
		close;
	}
	}
	switch(select("Mudar de classe para Noviço.:Saber os requerimentos para se tornar um Noviço.:Sair.")) {
	case 1:
		if(Class != Job_Novice) {
			mes "[Tomas Cerbantes]";
			if(Class == Job_Acolyte) {
				mes "Você está se sentindo bem hoje? Eu posso dizer pelo seu traje que você já é um Noviço. Você não está brincando, você está?";
			}else{
				mes "Sinto muito, mas parece que você já tem a sua própria classe, não é?";
			}
			close;
		}else{
			mes "[Tomas Cerbantes]";
			mes "Você realmente deseja se tornar um servo de Deus?";
			mes "Vamos ver se você está pronto para isso ou não... Hum...";
			next;
			if (getskilllv("NV_BASIC") < 9) {
				mes "[Tomas Cerbantes]";
				mes "Hã?! Você ainda não dominou as habilidades básicas?! Você tem um longo caminho a percorrer! Venha novamente depois de aumentar o seu nível de classe!";
				close;
			}
			mes "[Tomas Cerbantes]";
			mes "Hum... o seu nível de classe é suficiente...";
			mes "Bom. Agora vou dar-lhe a qualificação para se tornar um Noviço.";
			next;
			mes "[Tomas Cerbantes]";
			mes "Lembre-se de sempre ser grato a Deus, que cuida de nós o tempo todo. No caos e nos tempos de dificuldade enfrente tudo com fé inabalável.";
			jobchange Job_Acolyte;
			getitem 1545,1;	// Maça de Iniciante
			next;
			mes "[Tomas Cerbantes]";
			mes "Por último, quero sinceramente parabenizá-lo por ser perseverante no seu julgamento de penitência.";
			close;
		}
	case 2:
		mes "[Tomas Cerbantes]";
		mes "Você gostaria de se tornar um Noviço?";
		mes "Então, você deve preencher os seguintes requisitos pensando que essas são as práticas dadas por Deus.";
		next;
		mes "[Tomas Cerbantes]";
		mes "Primeiro, você tem que chegar pelo menos no Nível de Classe de Aprendiz 10 e aprender todas as habilidades básicas.";
		mes "Esta é a coisa mais básica que fazer, por isso é necessário considerá-la como forma de treinamento para você mesmo.";
		next;
		mes "[Tomas Cerbantes]";
		mes "Quando você achar que cumpriu esse requisito volte aqui novamente. Então você irá ter uma classe santa em que pode espalhar a vontade de Deus.";
		close;
	case 3:
		close;
	}
}

prt_fild03,365,255,2	script	Irmão Simão#nov	89,{
	mes "[Simão]";
	if (BaseJob == Job_Novice) {
		if (job_acolyte_q == 6) {
			mes "Por favor, tome cuidado. Eles devem saber que você me conheceu quando chegar ao Santuário de Prontera.";
			next;
			mes "[Simão]";
			mes "Enviei um pombo correio com uma mensagem. Espero que ele chegue em segurança...";
			close;
		}
		if (job_acolyte_q != 0) {
			if (job_acolyte_q == 2) {
				mes "Hã...? Você deve ser a pessoa que aspira a se tornar um Noviço. Eu já recebi a notícia do Santuário que você estaria vindo.";
				next;
				mes "[Simão]";
				mes "Agora, seu nome é " + strcharinfo(0) + ", certo? Excelente, obrigado por me visitar.";
				next;
				mes "[Simão]";
				mes "Eu acredito que você foi mandado pelo Bispo. Além disso, há muitas pessoas úteis no Santuário de Prontera.";
				next;
				mes "[Simão]";
				mes "Eu acho que não há realmente nenhuma necessidade de te ensinar muito. Além disso, eu tenho certeza que alguém da sua geração pode ter problemas para ouvir um velho como eu. Hahaha~";
				next;
				mes "[Simão]";
				mes "Ainda assim, as lições podem vir de lugares que você menos espera. Deus gosta de ensinar seus filhos de maneira estranha. Você vai ver.";
				next;
				mes "[Simão]";
				mes "Bem, eu vou enviar a mensagem dizendo que você veio me visitar. Então, agora você pode voltar para o Santuário de Prontera.";
				next;
				mes "[Simão]";
				mes "Adeus.";
				close2;
				savepoint "prt_fild03",361,255;
				set job_acolyte_q,6;
				end;
			}
			else {
				mes "Ah...";
				mes "Você é um dos";
				mes "candidatos a Noviço...?";
				mes "Vejamos...";
				next;
				mes "[Simão]";
				mes "Seu nome é " + strcharinfo(0) + "?";
				mes "Eu acho que o seu nome não";
				mes "está na minha lista. Humm...";
				next;
				mes "[Simão]";
				mes "Por que você não volta para o Santuário de Prontera e verifica novamente?";
				close;
			}
		}
		else {
			mes "Hum? O que te traz aqui? Este é um lugar muito perigoso para uma principiante como você!";
			close;
		}
	}
	else if (BaseJob == Job_Acolyte) callfunc "F_FatherRub";
	else {
		if (BaseJob == Job_Priest) {
			mes "Parabéns.";
			next;
			mes "[Simão]";
			mes "Bem-vindo. Sinta-se livre para sentar e contemplar a mensagem de Deus comigo. Este lugar é bonito, mesmo se o perigo acompanha a sua sensação de serenidade...";
			close;
		}
		else {
			mes "Nossa...";
			mes "Você veio para até aqui para treinar? Ou você é apenas um Andarilho?";
			next;
			mes "[Simão]";
			mes "Seja você quem for, por favor cuide de si mesmo. Os monstros aqui são surpreendentemente fortes, ao contrário de sua aparência bonita.";
			close;
		}
	}
}

moc_fild07,41,355,4	script	Irmã Matilda#2nov	95,{
	mes "[Matilda]";
	if (BaseJob == Job_Novice) {
		if (job_acolyte_q == 7) {
			mes "Vou enviar um pombo-correio para o Santuário de Prontera. Quando você voltar, o bispo já deverá ter recebido a minha mensagem.";
			next;
			mes "[Matilda]";
			mes "Vou orar a Deus, e espero que você se torne um Noviço em breve.";
			close;
		}
		if (job_acolyte_q != 0) {
			if (job_acolyte_q == 3) {
				mes "Ah, você deve ser um dos candidatos a Noviço. Eu desejo sinceras boas-vindas.";
				next;
				mes "[Matilda]";
				mes "Qual é o seu nome? " + strcharinfo(0) + "? Vejamos... Ah, você está na minha lista.";
				next;
				mes "[Matilda]";
				mes "Vou enviar uma mensagem para o Santuário, confirmando que você, " + strcharinfo(0) + " me visitou e completou sua penitência.";
				next;
				mes "[Matilda]";
				mes "Por favor, retorne ao Santuário de Prontera e fale com o bispo responsável.";
				close2;
				savepoint "moc_fild07",35,355;
				set job_acolyte_q,7;
				end;
			}
			else {
				mes "Ah...!";
				mes "Você deve ser um";
				mes "dos candidatos a Noviço.";
				mes "Eu desejo sinceras boas-vindas.";
				next;
				mes "[Matilda]";
				mes "Agora, qual seu nome?";
				mes "" + strcharinfo(0) + "? Vejamos...";
				next;
				mes "[Matilda]";
				mes "Hum...";
				mes "Parece que o seu nome";
				mes "não está na minha lista...";
				next;
				mes "[Matilda]";
				mes "Talvez você deva retornar ao Santuário de Prontera e verificar o destino do seu julgamento de penitência mais uma vez.";
				close;
			}
		}
		else {
			mes "...";
			close;
		}
	}
	else if (BaseJob == Job_Acolyte) callfunc "F_MotherMart";
	else {
		if (BaseJob == Job_Priest) {
			mes "Olá~";
			next;
			mes "[Matilda]";
			mes "Eu certamente espero que você esteja gostando de estar na graça de Deus.";
			close;
		}
		else {
			mes "Que Deus";
			mes "esteja com você...";
			close;
		}
	}
}

prt_fild00,208,218,6	script	Irmão Bartolomeu#3nov	98,{
	mes "[Bartolomeu]";
	if (BaseJob == Job_Novice) {
		if (job_acolyte_q == 8) {
			mes "O que?";
			next;
			mes "[Bartolomeu]";
			mes "Você tem mais algo a tratar?! Você não tem! Volte para o Santuário agora!";
			close;
		}
		if (job_acolyte_q != 0) {
			if (job_acolyte_q == 4) {
				mes "Ei.";
				mes "O que quer que você seja";
				mes "você parece um";
				mes "dos candidatos a Noviço.";
				mes "Certo?";
				next;
				mes "[Bartolomeu]";
				mes "Nada mal, nada mal. Você resistiu ao julgamento de penitência muito bem.";
				mes "Então, qual é o seu nome?";
				next;
				mes "[Bartolomeu]";
				mes "" + strcharinfo(0) + ", huh?";
				next;
				mes "[Bartolomeu]";
				mes "Ok. Vou mandar uma mensagem para o Santuário que você, " + strcharinfo(0) + ", veio me visitar.";
				next;
				mes "[Bartolomeu]";
				mes "Agora volte para o Santuário e acabe se tornando um Noviço.";
				close2;
				savepoint "prt_fild00",206,230;
				set job_acolyte_q,8;
				end;
			}
			else {
				mes "Ei.";
				mes "Você parece um dos candidatos a Noviço. Eu estou certo?";
				next;
				mes "[Bartolomeu]";
				mes "Você percorreu todo o caminho aqui de Prontera. Então como você se chama?";
				next;
				mes "[Bartolomeu]";
				mes "" + strcharinfo(0) + ", hum? Por que o seu nome não está na minha lista?";
				next;
				mes "[Bartolomeu]";
				mes "Você provavelmente cometeu um erro. Volte para o Santuário, e verifique com o bispo.";
				close;
			}
		}
		else {
			mes "Você...";
			mes "Aprendiz.";
			mes "Há algo que";
			mes "você queira falar comigo?";
			close;
		}
	}
	else if (BaseJob == Job_Acolyte) callfunc "F_FatherYos";
	else {
		if (BaseJob == Job_Priest) {
			mes "Ei...";
			next;
			mes "[Bartolomeu]";
			mes "Se você gosta, sente-se aqui comigo para meditarmos as grandes verdades. A majestade de Deus é verdadeiramente inspiradora...";
			close;
		}
		else {
			mes "Você tem algo a dizer? Porque infelizmente para você";
			mes "eu não tenho nenhuma resposta.";
			close;
		}
	}
}