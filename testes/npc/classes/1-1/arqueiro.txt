//    ________________________________________________________
//   /                                                        \
//   |       _           _   _   _                            |
//   |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
//   |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
//   |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
//   |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
//   |                                                        |
//   |                    brAthena Script                     |
//   |--------------------------------------------------------|
//   | Nome do Script: Quest do Arqueiro                      |
//   |--------------------------------------------------------|
//   | Criado por: eA/rA/brA Dev. Team                        |
//   |--------------------------------------------------------|
//   | Versões eA/rA/brA: 1.9/1.0/1.1                         |
//   |--------------------------------------------------------|
//   | Descrição: Mudança de Classe para Arqueiro.            |
//   |--------------------------------------------------------|
//   | Changelog:                                             |
//   | 1.0 Criado Cabeçalho [Megasantos]                      |
//   | 1.1 Tradução PT-BR [Holy e Protimus]                   |
//   |--------------------------------------------------------|
//   | - Anotações                                            |
//   \________________________________________________________/

payon_in02,64,71,4	script	Guia dos Arqueiros#arqueiro	85,{
  if(Upper == 1) {
		if (Class == Job_Novice_High && (ADVJOB == Job_Sniper || ADVJOB == Job_Clown || ADVJOB == Job_Gypsy)) {
			mes "[Guia dos Arqueiros]";
			mes "Ei, eu te conheço.";
			mes "Você já fez esse teste";
			mes "antes, não foi?";
			next;
			mes "[Guia dos Arqueiros]";
			mes "Ah, você deve ter ido";
			mes "a Valhalla renascer.";
			mes "Uau, é tão impressionante!";
			next;
			if (getskilllv("NV_BASIC") < 9) {
				mes "[Guia dos Arqueiros]";
				mes "Err...";
				mes "É melhor aprender todas as habilidades básicas antes de se tornar um Arqueiro.";
				next;
				mes "[Guia dos Arqueiros]";
				mes "Certo, até logo.";
				close;
			}
			mes "[Guia dos Arqueiros]";
			mes "Bem então. Eu não";
			mes "preciso dizer mais nada.";
			mes "Eu sei que você pode se tornar um grande Arqueiro...";
			next;
			skill 143,0,0;
			jobchange Job_Archer_high;
			skill 147,1,0;
			skill 148,1,0;
			mes "[Guia dos Arqueiros]";
			mes "Embora não haja uma premiação";
			mes "especial por enquanto, eu espero que você entenda. Tenha cuidado.";
			close;
		  }
			mes "[Guia dos Arqueiros]";
			mes "Hã...?";
			mes "Ei, o que";
			mes "você está fazendo aqui...?";
			next;
			mes "[Guia dos Arqueiros]";
			mes "Eu posso dizer que você não está destinado para ser um Arqueiro. Algo me da a entender que você deve procurar fazer uma outra coisa...";
			close;
}
	mes "[Guia dos Arqueiros]";
	mes "Prazer em conhecê-lo. Como posso ajudá-lo?";
	next;
	switch(select("Eu quero me tornar um Arqueiro.:Eu preciso saber dos requerimentos, por favor.:Nada, obrigado.")) {
	case 1:
		if(Class != Job_Novice) {
			mes "[Guia dos Arqueiros]";
			if(Class == Job_Archer)
			mes "Haha, você está brincando comigo..";
			else
			mes "Eu sinto muito, mas apenas Aprendizes podem mudar de classe. Você já tem a sua própria classe, não é?";
			close;
			} if ( !checkre(0) ){
		  if (job_archer_q == 0) {
			mes "[Guia dos Arqueiros]";
			mes "Você quer ser um arqueiro?";
			mes "Se você quer, preencha este formulário.";
			next;
			if (select("Inscrever-se.:Cancelar") == 1) {
				set job_archer_q,1;
				setquest 1004;
				mes "[Guia dos Arqueiros]";
				mes "Ok, assine aqui... Certo... Hum, você será promovido de classe assim que cumprir os pré-requisitos necessários.";
				mes "Se você acha que está pronto, podemos começar agora.";
				mes "Você está pronto?";
				next;
				if (select("Sim!:Não, não estou pronto ainda.") == 1) {
					mes "[Guia dos Arqueiros]";
					mes "Vamos ver então.";
					next;
				} else {
					mes "[Guia dos Arqueiros]";
					mes "Compreendo...";
					mes "Se você quiser saber os pré-requisitos para se tornar um Arqueiro, apenas fale comigo.";
					close;
				}			
			}		
			mes "[Guia dos Arqueiros]";
			mes "Até mais!";
			close;
     }  
		mes "[Guia dos Arqueiros]";
		mes "Você se chama "+strcharinfo(0)+"?";
		next;
		mes "[Guia dos Arqueiros]";
		if (getskilllv("NV_BASIC") < 9) {
			mes "O seu nível como Aprendiz está abaixo do necessário...";
			mes "Você deve chegar ao nível 10 de classe para se tornar um Arqueiro.";
			close;
		}
		if (job_archer_q == 1) {
			set .@archer_item1,countitem(1066) * 5;
			set .@archer_item2,countitem(1067) * 3;
			set .@archer_item3,countitem(1068) * 2;
			set .@archer_item4,countitem(1019);
			set .@total_archer,.@archer_item1 + .@archer_item2 + .@archer_item3 + .@archer_item4;
			set .@total_archer2,(((.@archer_item2 + .@archer_item3) * 2) + .@archer_item4);
			mes "Ótimo! Bom trabalho!";
			next;
			mes "[Guia dos Arqueiros]";
			mes "Agora vamos ver se você trouxe Troncos suficientes para fazermos um Arco.";
			next;
			mes "[Guia dos Arqueiros]";
			if (countitem(1066) == 0 && countitem(1067) == 0 && countitem(1068) == 0 && countitem(1019) == 0) {
				mes "...Hum...";
				mes "Você não me trouxe nada!";
				close;
			}
			if (countitem(1066)) mes "Tronco Tipo 1: "+countitem(1066)+" unidades: "+.@archer_item1+".";
			if (countitem(1067)) mes "Tronco Tipo 2: "+countitem(1067)+" unidades: "+.@archer_item2+".";
			if (countitem(1068)) mes "Tronco Tipo 3: "+countitem(1068)+" unidades: "+.@archer_item3+".";
			if (countitem(1019)) mes "Tronco Tipo 4: : "+countitem(1019)+" unidades: "+.@archer_item4+".";
			if (.@total_archer < 25) {
				mes "O seu total de pontos foi: ^FF0000"+.@total_archer+"^000000/40";
				next;
				mes "[Guia dos Arqueiros]";
				mes "Menos de 25?!?! Se esforce um pouco mais...";
				close;
			} else {
				mes "O seu total de pontos foi: ^0000FF"+.@total_archer+"^000000/40";
				next;
				mes "[Guia dos Arqueiros]";
				if (.@total_archer > 40) mes "Nossa! Mais de 40! Fantástico! Meus parabéns!";
				else mes ( .@total_archer > 30 ) ? "Mais de 30! Ótimo! Parabéns.":"Passou raspando... Mas tudo bem!";
			}
			next;
			mes "[Guia dos Arqueiros]";
			mes "Vou mandar o material para o Departamento de Produção de Arcos.";
			if (countitem(1066)) delitem 1066,countitem(1066); // Tronco de Alta Qualidade
			if (countitem(1067)) delitem 1067,countitem(1067); // Tronco Sólido
			if (countitem(1068)) delitem 1068,countitem(1068); // Tronco Estéril
			if (countitem(1019)) delitem 1019,countitem(1019); // Tronco
		}
		next;
		callfunc "Job_Change",Job_Archer;
		callfunc "F_ClearJobVar";
		completequest 1004;
		mes "[Guia dos Arqueiros]";
		mes "Parabéns, você passou no teste! Vou nomeá-lo um Arqueiro!";
		next;
		mes "[Guia dos Arqueiros]";
		mes "Parabéns! Nós esperamos que você contribua para a grandiosidade de nossa Guilda!";
		next;
		mes "[Guia dos Arqueiros]";
		mes "Ah! Seu Arco chegou do Departamento de Produção, aqui está ele.";
		getitem 1702,1; // Arco [4]
		getitem 1750,.@total_archer2; // Flecha
		next;
		mes "[Guia dos Arqueiros]";
		mes "Agora você é um Arqueiro legítimo, com arco, flecha e tudo mais!";
		mes "Boa Sorte em sua jornada pelo mundo, espero que você se torne o melhor de todos os Arqueiros!";
		close;
		}
		  mes "[Guia dos Arqueiros]";
			mes "Você é... "+strcharinfo(0)+". Certo?";
			next;
			if (getskilllv("NV_BASIC") < 9) {
			mes "[Guia dos Arqueiros]";
			mes "Bem, você deve possuir o nível de habilidades requerido.";
			mes "Seu nível de classe deve ser de pelo menos ^4A4AFF10^000000 e seu nível de habilidades básicas deve ser de ^4A4AFFNível 9^000000";
			next;
			mes "[Guia dos Arqueiros]";
			mes "Um Arqueiro precisa de uma concentração extremamente elevada, por isso nós não aceitamos aqueles que têm pouca paciência.";
			close;
      }
			mes "[Guia dos Arqueiros]";
			mes "Seu Nível de Habilidades Básicas está completo..";
			mes "....Hum~~ agora você está pronto para se tornar um Arqueiro. Vou providenciar a mudança imediatamente.";
			next;
			mes "[Guia dos Arqueiros]";
			mes "Parabéns! Agora você é um Arqueiro! Também, esperamos que você participe ativamente em muitos programas de renascimento da Guilda de Arqueiros.";
			mes "Ah, itens chegaram vindos do Departamento de Produção. Aqui, pegue! São todos seus!";
			jobchange Job_Archer;
			getitem 1742,1;    // Arco Composto de Iniciante
			getitem 12004,1;   // Aljave
			getitem 12009,1;   // Aljave de Flechas de Prata
			getitem 12008,1;   // Aljave de Flechas de Fogo
			next;
			mes "[Guia dos Arqueiros]";
			mes "Tendo um arco e flechas, agora você pode se considerar um verdadeiro Arqueiro.";
			mes "Se você abrir sua aljave de flechas, há flechas nela que podem ser equipadas por você.";
			mes "Bem, espero ouvir boas notícias de você. É hora de dizer adeus.";
			mes "Adeus.";
			close;

	case 2:
	if( !checkre(0) ) {
		mes "[Guia dos Arqueiros]";
		mes "I will explain the requirements for being an Archer.";
		if (BaseJob != Job_Novice) {
			if (BaseJob == Job_Archer) {
				next;
				mes "[Guia dos Arqueiros]";
				mes "But...";
				mes "You're already an Archer. You should know these already...";
			} else {
				next;
				mes "[Guia dos Arqueiros]";
				mes "Wait a second. You've chosen a different job already. You don't need to know this~";
			}
			mes "So...Yeah...no real reason to tell you the requirements...";
		}
		next;
		mes "[Guia dos Arqueiros]";
		mes "First of all, you have to the Job Level 9 as a Novice, and know all of the Basic Skills.";
		next;
		mes "[Guia dos Arqueiros]";
		mes "An Archer needs extremely high concentration and reflexes, so we do not accept those who have little patience.";
		next;
		mes "[Guia dos Arqueiros]";
		mes "You also have to gather ^FF0000Trunks^000000. There are 4 different types of Trunks, each of differing quality. You'll be given different grades for your Trunks, depending on their quality.";
		next;
		mes "[Guia dos Arqueiros]";
		mes "In order to become an Archer, you must receive a grade of at least ^0000FF25^000000 points out of 40. You can get Trunks from 'Willow,' the tree. Be careful, though. They can be tough monsters.";
		close;
	  } 
	  mes "[Guia dos Arqueiros]";
		mes "Bem, você deve possuir o nível de habilidades requerido.";
		mes "Seu nível de classe deve ser de pelo menos ^4A4AFF10^000000 e seu nível de habilidades básicas deve ser de ^4A4AFFNível 9^000000";
		next;
		mes "[Guia dos Arqueiros]";
		mes "Um Arqueiro precisa de uma concentração extremamente elevada, por isso nós não aceitamos aqueles que têm pouca paciência.";
		close;
 
	case 3:
		mes "[Guia dos Arqueiros]";
		mes "Se você tiver alguma dúvida, sinta-se a vontade para perguntar.";
		close;
}
}