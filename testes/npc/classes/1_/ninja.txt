//    ________________________________________________________
//   /                                                        \
//   |       _           _   _   _                            |
//   |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
//   |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
//   |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
//   |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
//   |                                                        |
//   |                    brAthena Script                     |
//   |--------------------------------------------------------|
//   | Nome do Script: ninja.txt                              |
//   |--------------------------------------------------------|
//   | Criado por: eA/rA/brA Dev. Team                        |
//   |--------------------------------------------------------|
//   | Versões eA/rA/brA: 1.6/1.2                             |
//   |--------------------------------------------------------|
//   | Descrição: Mudança de Classe para Ninja.               |
//   |--------------------------------------------------------|
//   | Changelog:                                             |
//   | 1.0 Criado Cabeçalho [Megasantos]                      |
//   | 1.1 Tradução e Revisão [lututuiBR]                     |
//   | 1.2 Otimização [lututuiBR]                             |
//   |--------------------------------------------------------|
//   | - Anotações                                            |
//   \________________________________________________________/

alberta,30,65,3	script	Akagi	730,{
	if (Class != Job_Novice) {
		mes "[Akagi]"+"\nHmm...\nVocê e eu...\nSomos bastante parecidos em termos de habilidade de combate.\nTalvez possamos treinar juntos algum dia.";
		close;
	}
	if (JobLevel < 10) {
		mes "[Akagi]"+"\nHm? Não posso te dar nenhum serviço até que se torne um pouco mais forte...";
		close;
	}
	mes "[Akagi]"+"\nHumm...\nVocê deve ter vindo aqui sentindo que alguém estava esperando por você.\nDiga-me, você busca o caminho da paciência?";
	next;
	if(select("Não:Sim") == 1) {
		mes "[Akagi]"+"\nEntendo.\nCada um por si, suponho.\nCuide-se.";
		close;
	}
	mes "[Akagi]"+"\nMuito bem.\nEntão, deixe-me levá-lo àquele caminho logo ali.";
	close2;
	switch(rand(3)) {
		case 1: warp "amatsu",170,229; end;
		case 2: warp "amatsu",216,188; end;
		default: warp "amatsu",178,176; end;
	}
}

que_ng,30,65,3	script	Cougar#nq	730,{
	if (Upper == 2) {
		mes "[Cougar]"+"\nEu... Eu nunca vi uma criancinha tão poderosa quanto você!\nS-se afaste, sua aberração da natureza!";
		close;
	}
	if (Class == Job_Ninja) {
		mes "[Cougar]"+"\nComo tem passado?\nTreine duro: você quer ser capaz de desaparecer sem deixar rastros.\nSe você puder fazer isso, terá o respeito de qualquer Ninja~";
		close;
	} else if (Class == Job_Novice && JobLevel < 10) {
		mes "[Cougar]"+"\nHm?\nVocê veio para aprender como se tornar um Ninja?\nVocê não é suficientemente experiente ainda, então volte depois que lutar com monstros lhe for mais familiar.";
		close;
	} else if (Class == Job_Novice && JobLevel == 10) {
		if (NINJ_Q == 0) {
			mes "["+strcharinfo(0)+"]"+"\nCom licença.\nO-olá?";
			next;
			mes "[Cougar]"+"\n...............................\nComo fez isso?";
			next;
			mes "["+strcharinfo(0)+"]"+"\nFiz o quê?\nEu não fiz nada, eu acho que não...";
			next;
			mes "[Cougar]"+"\nC-como você é capaz de me vez? Era suposto que eu estivesse invisível a olho nu!\nAh, agora entendi.\nWildcat Joe deve ter te mandado para me matar!\nNão cairei em seus truques! Morra!";
			next;
			mes "["+strcharinfo(0)+"]"+"\nE-espere!\nEu nem mesmo sei quem sei quem Wildcat Joe é!\nAcalme-se, não há motivo para ficar violento!";
			next;
			mes "[Cougar]"+"\nQuê...?\nComo se esquivou de todos os meus ataques?\nVocê tem algum talento, eu admito.";
			next;
			mes "["+strcharinfo(0)+"]"+"\n...............................\nEu vim aqui esperando mudar minha classe para Ninja.";
			next;
			mes "[Cougar]"+"\n...Oh. Isso é tudo?\nHmm, você tem grande potencial, mas não posso te ajudar agora.\nTenho inimigos demais, e não posso baixar a guarda nem por um segundo.";
			next;
			mes "[Cougar]"+"\nAquele Wildcat Joe é completamente implacável...!\nEle poderia atacar a qualquer hora!\nEle faria qualquer coisa para alcançar a vitóra sobre seus inimigos!";
			next;
			mes "[Cougar]"+"\nEspere, espere, acabei de pensar em algo.\nTalvez você possa me ajudar.\nFaça o que peço, e irei ensiná-lo algumas de minhas habilidades se você realmente quiser se tornar um Ninja.";
			next;
			if(select("Com certeza!:Não, obrigado.") == 2) {
				mes "[Cougar]"+"\nHm? Bem, tudo bem.\nEntretanto, não entendo porque não podemos nos ajudar neste pequeno dilema...";
				close;
			}
			mes "[Cougar]"+"\nÓtimo!\nAgora, eu queria perguntar ask Wildcat Joe se ele concordaria com uma trégua temporária.\nEstou ciente que ambos estamos sem armas, então deveríamos nos equipar melhor antes.";
			next;
			mes "[Cougar]"+"\nPor favor, pegue esta carta e entregue-a a Wildcat Joe, em Einbroch.\nEle é um mestre dos disfarces, então mantenha um olhar atento a ele.\nAh, e procure-o em um local alto.";
			next;
			mes "[Cougar]"+"\nSim, Wildcat Joe sempre teve uma queda por se esconder em lugares altos.\nDe qualquer maneira, depois de entregar-lhe a carta, volte e me diga sua resposta.";
			set NINJ_Q,1;
			setquest 6015;
			close;
		} else if(NINJ_Q == 1) {
			mes "[Cougar]"+"\nMesmo que esta tarefa não seja tão urgente, por favor, corra para Einbroch e entregue minha carta para Wildcat Joe.";
			close;
		} else if(NINJ_Q == 2) {
			mes "[Cougar]"+"\nVocê entregou aquela carta para Wildcat Joe?\nEu ainda preciso saber a resposta dele para minha proposta de trégua.\nDe qualquer maneira, veja se consegue forçá-lo a isso.";
			close;
		} else if(NINJ_Q == 3) {
			mes "[Cougar]"+"\nAh, você voltou.\nEntão, Wildcat Joe mandou você de volta com a resposta dele?\nÓtimo, ótimo, por favor, leia-a para mim.";
			next;
			mes "[Cougar]"+"\nQuê...?!\nComo ele poderia rejeitar minha proposta de trégua?!\nIsto só pode significar que ele fez outra Kunai.\nPoxa! Tenho que alcançá-lo ou estarei arruinado!";
			next;
			mes "[Cougar]"+"\nOuça, você tem que me ajudar de novo!\nPreciso que você me traga alguns materiais para que eu possa criar minha própria Kunai para lutar com Wildcat Joe.\nEntão, cumprirei nosso trato e mudarei sua classe para Ninja.";
			next;
			mes "[Cougar]"+"\nTudo o que precisa me trazer é\n^3355FF5 Cyfars^000000 e\n^3355FF1 Fracon^000000.\nPor favor, consiga-os o mais rápido que puder!";
			next;
			mes "["+strcharinfo(0)+"]"+"\nHuh? Que engraçado, Wildcat Joe me pediu para conseguir os mesmos materiais.";
			next;
			mes "[Cougar]"+"\nMaldição! Então isso significa...\n Que você ajudou Joe em criar sua Kunai! Não!\nEu deveria ter pensado nisso antes!\nBem, é tarde demais agora. Apenas apresse-se!";
			set NINJ_Q,4;
			changequest 6017,6018;
			close;
		} else if(NINJ_Q == 4) {
			if (countitem(7053) < 5 || !(countitem(1010))) {
				mes "[Cougar]"+"\nSe apresse e traga\n^3355FF5 Cyfars^000000 e\n^3355FF1 Fracon^000000 para mim,\nentão eu posso criar minha própria Kunai para usar contra Wildcat Joe!";
				close;
			}
			if (Skillpoint) {
				mes "[Cougar]"+"\nWhoa, whoa...\nVocê ainda tem alguns Pontos de Habilidade restantes.\nÉ melhor gastar todos eles antes de mudar de classe, certo?";
				close;
			}
			mes "[Cougar]"+"\nAh, você está de volta com tudo que preciso.\nVoltou antes do que eu esperava, eh?\nÓtimo, como prometido, te transformarei em um Ninja.";
			next;
			mes "[Cougar]"+"\nPermita que eu me apresente formalmente.\nEu sou o Mestre Ninja Cougar do Grupo Ninja Touga, e estou encarregado do grupo de busca para encontrar Sir Kazma.";
			next;
			mes "[Cougar]"+"\nSir Kazma é o chefe do meu vilarejo, mas ele fugiu.\nIsto resultou em um conflito interno entre o Grupo Ninja.\nAs coisas estão bem instáveis agora...";
			next;
			mes "[Cougar]"+"\nInicialmente, não queria te aceitar como um Ninja, por causa dessa situação complicada.\nContudo, você provou que é realmente digno de se juntar ao grupo dos Ninjas.";
			next;
			mes "[Cougar]"+"\nDe acordo com a carta dele, até mesmo Joe reconhece seu valor.\nBasta lembrar que, como um Ninja, sua missão é sua maior prioridade. Mas não deixe os objetivos da missão tornem-se mais importantes que sua consciência.";
			next;
			mes "[Cougar]"+"\n''Sigilo acima de tudo.''\nPara manter nossos segredos nas sombras, você pode somente comprar ou vender armas Ninja em comerciantes autorizados.\nPor favor, lembre-se disso.";
			next;
			mes "[Cougar]"+"\nA partir de hoje, você é um membro do esplêndido Grupo Ninja Touga.\nSeja tão ágil como o vento, e tão silencioso como a o cair das sombras.";
			callfunc "Job_Change",Job_Ninja;
			set NINJ_Q,5;
			delitem 7053,5; // Cyfar
			delitem 1010,1; // Fracon
			getitem 13010,1;	// Asura
			completequest 6018;
			close;
		}
		mes "[Cougar]"+"\nComo tem passado?\nTreine duro: você quer ser capaz de desaparecer sem deixar rastros.\nSe você puder fazer isso, terá o respeito de qualquer Ninja~";
		close;
	}	
	mes "[Cougar]"+"\nQuê...?\nComo você foi capaz de me encontrar nas sombras?!\nVocê deve ser mais que um aventureiro comum, eh?";
	close;
}

einbroch,184,194,3	script	Homem Suspeito#nq	 	881,{
	if (NINJ_Q == 1) {
		mes "[Homem Suspeito]"+"\nEu viajei para muitos países, mas nunca estive em um prédio tão alto como a Torre de Einbroch.\nTodos os prédios na minha cidade natal são minúsculos em comparação...";
		next;
		mes "["+strcharinfo(0)+"]"+"\nOh, você é de Amatsu?\nEstou procurando alguém de lá que é chamado Wildcat Joe.";
		next;
		mes "[Homem Suspeito]"+"\nNão... Não, na verdade sou de Izlude, e estou aqui em Einbroch somente por alguns minérios.\nDiga-me, por que está procurando por Wildcat Joe?";
		next;
		mes "["+strcharinfo(0)+"]"+"\nBem, preciso entregar esta carta para ele e obter sua resposta para que eu possa me tornar um Ninja.";
		next;
		mes "[Homem Suspeito]"+"\nSério?\nPensando bem, eu acho que já o encontrei uma ou duas vezes nesta cidade.\nNo entanto, ele prefere ser chamado de ''Red Leopard Joe,'' ao invés de ''Wildcat Joe.''";
		next;
		mes "[Homem Suspeito]"+"\nEu realmente quero te ajudar a encontrá-lo, mas primeiro eu preciso encontrar alguns minérios que estou procurando.\nSe não se importar, poderia me ajudar?\nDepois posso te ajudar a encontrar Joe.";
		next;
		if(select("Não se preocupe, eu o encontrarei sozinho.:Claro, eu te ajudarei.") == 1) {
			mes "[Homem Suspeito]"+"\nTem certeza...?\nRed Leopard Joe é realmente um mestre dos disfarces.\nVocê precisará de toda ajudar que puder para encontrá-lo...";
			close;
		}
		mes "[Homem Suspeito]"+"\nÓtimo, estou feliz por ouvir isso.\nPor favor, ajude-me a encontrar\n^3355FF5 Cyfars^000000 e\n^3355FF1 Fracon^000000.";
		set NINJ_Q,2;
		changequest 6015,6016;
		close;
	} else if(NINJ_Q == 2) {
		if (countitem(7053) < 5 || !(countitem(1010))) {
			mes "[Homem Suspeito]"+"\nPor favor, traga\n^3355FF5 Cyfars^000000 e\n^3355FF1 Fracon^000000 para mim o mais rápido que puder.\nEntão, eu posso te ajudar a encontrar Red Leopard Joe.";
			close;
		}
		mes "[Homem Suspeito]"+"\nBom, muito bom. Você trouxe os minerais...\nAgora, é minha vez de te ajudar.\nAqui, deixe-me ver essa carta.";
		next;
		mes "["+strcharinfo(0)+"]"+"\n?????!!";
		next;
		mes "[Homem Suspeito]"+"\nPor quê? Você não trouxe essa carta de Cougar para mim?";
		next;
		mes "["+strcharinfo(0)+"]"+"\nVocê é..\nVocê é Wildcat Joe?";
		next;
		mes "[Homem Suspeito]"+"\n...Sim, mas prefiro ser chamado Red Leopard Joe.\n Cougar o mandou a mim, certo?\n Ele é o único que me chama assim.\nEntão você quer ser um ninja, eh? Hmm, está bem.";
		next;
		mes "[Red Leopard Joe]"+"\nSe você quiser ser um Ninja, deve sempre ser cuidadoso com o que vê e em o quem confia.\nNão se esqueça de que, se os seus segredos forem descobertos, então você está acabado como Ninja.";
		next;
		mes "[Red Leopard Joe]"+"\nLembre-se de se mover rapidamente, e de sempre desaparecer sem deixar rastros.\nPermanecer oculto nas sombras é realmente nosso poder supremo.";
		next;
		mes "["+strcharinfo(0)+"]"+"\nEntendo...\n...........";
		next;
		mes "[Red Leopard Joe]"+"\nPor agora, deixe-me ler esta carta. Vamos ver...\nHm. Pensei que Cougar queria me desafiar de novo, mas na verdade ele quer uma trégua temporária? Hah!";
		next;
		mes "[Red Leopard Joe]"+"\nObrigado por sua ajuda, agora que tenho minérios, preciso construir uma Kunai!\nHahaha! Não concordarei com uma trégua quando eu tenho a vantagem!";
		next;
		mes "[Red Leopard Joe]"+"\nDe qualquer maneira, deixe-me escrever minha resposta para ele.\nTambém vou escrever uma recomendação para você...\nAcho que você vai se tornar um excelente Ninja, mesmo que eu tenha acabado de te enganar.";
		next;
		mes "["+strcharinfo(0)+"]"+"\n......\n.........\n............";
		next;
		mes "[Red Leopard Joe]"+"\nAqui está.\nPor favor, leve esta carta para Cougar.\nDemoraria um pouco para voltar para Amatsu, então deixe-me mandá-lo diretamente para lá...";
		delitem 1010,1;	// Fracon
		delitem 7053,5;	// Cyfar
		set NINJ_Q,3;
		changequest 6016,6017;
		close2;
		warp "amatsu",113,127;
		end;
	} else if(NINJ_Q == 3) {
		mes "[Red Leopard Joe]"+"\nEh? Não sei o que aconteceu, mas parece que você ainda não entregou minha resposta para Cougar.\nDevo enviar-lhe diretamente para Amatsu agora?";
		next;
		if(select("Não, obrigado.:Sim, por favor.") == 1) {
			mes "[Red Leopard Joe]"+"\nCerto.\nBem, eu estava só tentando poupar algum tempo seu.";
			close;
		}
		mes "[Red Leopard Joe]"+"\nOkay, então.\nAdeus por enquanto.";
		close2;
		warp "amatsu",113,127;
		end;
	} else if(NINJ_Q == 4) {
		mes "[Red Leopard Joe]"+"\nCougar te pediu para obter alguns materiais também?\nOh, bem, suponho que não posso culpá-lo.\nAliás, devo ser capaz de vencê-lo em uma luta justa~";
		close;
	} else if(NINJ_Q == 5 && Class == Job_Ninja) {
		mes "[Red Leopard Joe]"+"\nOh, você é um Ninja~\nEspero que continue a treinar e maximizar todas as habilidades de Ninja que puder.\nSempre se lembre de misturar-se com as sombras.";
		close;
	}
	mes "[Turista]"+"\nEu viajei para muitos países, mas nunca estive em um prédio tão alto como a Torre de Einbroch.\nTodos os prédios na minha cidade natal são minúsculos em comparação...";
}