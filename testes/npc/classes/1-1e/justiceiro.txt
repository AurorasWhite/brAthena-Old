//    ________________________________________________________
//   /                                                        \
//   |       _           _   _   _                            |
//   |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
//   |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
//   |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
//   |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
//   |                                                        |
//   |                    brAthena Script                     |
//   |--------------------------------------------------------|
//   | Nome do Script: justiceiro.txt                         |
//   |--------------------------------------------------------|
//   | Criado por: eA/rA/brA Dev. Team                        |
//   |--------------------------------------------------------|
//   | Versões eA/rA/brA: 2.2/2.2/1.1                         |
//   |--------------------------------------------------------|
//   | Descrição: Mudança de Classe para Justiceiro.          |
//   |--------------------------------------------------------|
//   | Changelog:                                             |
//   | 1.0 Criado Cabeçalho [Megasantos]                      |
//   | 1.1 Tradução PT-BR [lututuiBR]
//   |--------------------------------------------------------|
//   | - Anotações                                            |
//   \________________________________________________________/

que_ng,152,167,3	script	Mestre Miller	901,{
	if (Upper == 2) {
		mes "[Mestre Miller]";
		mes "Bem, você não é uma";
		mes "adorável criancinha?";
		mes "Onde está sua mamãe?";
		mes "Esse lugar é perigoso, então,";
		mes "por favor, vá logo para casa, ok?";
		close;
	}
	if (Class == Job_Novice) {
		if (JobLevel < 10 || getskilllv("NV_BASIC") < 9) {
			mes "[Mestre Miller]";
			mes "Interessado em se tornar";
			mes "um Justiceiro, eh? Você tem";
			mes "potencial, mas não tem";
			mes "experiência suficiente.";
			mes "Treine mais um pouco,";
			mes "e depois volte, entendeu?";
			close;
		}
		if (GUNS_Q == 0) {
			mes "[Mestre Miller]";
			mes "Eu sou Miller, um treinador de";
			mes "Justiceiros e";
			mes "guardião em tempo integral de Lady";
			mes "Selena. Pois bem, do que você";
			mes "precisa? Se não for importante, então não tenho tempo a perder com você.";
			next;
			if(select("Nada.:Eu quero me tornar un Justiceiro.") == 1) {
				mes "[Mestre Miller]";
				mes "Não disperdice meu tempo.";
				mes "Se você quiser se tornar um";
				mes "Justiceiro, volte";
				mes "e fale comigo";
				close;
			}
			mes "[Mestre Miller]";
			mes "Hm. Você é muito jovem, mas";
			mes "seus olhos dizem que você é";
			mes "bastante ambicioso. Você terá que passar em nossa entrevista e curso";
			mes "educacional para se tornar um Justiceiro. Você quer se candidatar ao cargo?";
			next;
			if(select("Me de algum tempo para pensar.:Com certeza!") == 1) {
				mes "[Mestre Miller]";
				mes "Compreensível.";
				mes "Se você decidir que";
				mes "quer se tornar";
				mes "um Justiceiro, então";
				mes "me conte imediatamente.";
				mes "Eu te ajudarei a começar.";
				close;
			}
			mes "[Mestre Miller]";
			mes "Ótimo, ótimo. Muito bem, então";
			mes "vamos começar. Leve esta carta";
			mes "para o Sr. Sábio Chifre de Touro,";
			mes "em Payon. Ele é um xamã que irá";
			mes "julgar se você é ou não qualificado";
			mes "para se tornar um Justiceiro.";
			set GUNS_Q,1;
			setquest 6020;
			close;
		}
		else if(GUNS_Q == 1) {
			mes "[Mestre Miller]";
			mes "Leve essa carta de";
			mes "apresentação que escrevi";
			mes "para o Sr. Sábio Chifre de Touro,";
			mes "em Payon. Ele te testará";
			mes "para ver se você pode";
			mes "se tornar um Justiceiro.";
			close;
		}
		else if(GUNS_Q == 2) {
			mes "[Mestre Miller]";
			mes "Hmm... o Sábio Chifre de Touro";
			mes "te pediu para coletar os";
			mes "itens que precisa para fazer o";
			mes "amuleto? Hm. Eu acho que isso";
			mes "faz parte do teste.";
			close;
		}
		else if(GUNS_Q == 3) {
			mes "[Mestre Miller]";
			mes "O Sábio Chifre de Touro te pediu";
			mes "um pouco de leite?";
			mes "Ele deve gostar de você,";
			mes "já que está pedindo";
			mes "favores. Boa sorte, amigo.";
			close;
		}
		else if(GUNS_Q == 4) {
			mes "[Mestre Miller]";
			mes "Eu espero ouvir boas";
			mes "notícias de você em breve. Você";
			mes "sabe, eu não tenho dúvida que";
			mes "você irá se tornar um Justiceiro.";
			close;
		} else if(GUNS_Q == 5) {
			if (Skillpoint != 0) {
				mes "[Mestre Miller]";
				mes "Hey, você tem Pontos de";
				mes "Habilidades sobrando. Use-os";
				mes "antes de vir falar";
				mes "comigo de novo.";
				close;
			}
			mes "[Mestre Miller]";
			mes "Oh, você me trouxe um";
			mes "amuleto do Sábio Chifre de Touro?";
			mes "Já tem um tempo que ele não";
			mes "dá um pra ninguém...";
			mes "Estou muito orgulhoso de você!";
			next;
			mes "[Mestre Miller]";
			mes "Se o Sábio Chifre de Touro aprova,";
			mes "então eu não tenho motivos para";
			mes "rejeitar você. Tudo bem então, vou";
			mes "promovê-lo a Justiceiro.";
			mes "Mas primeiro, deixe-me explicar";
			mes "sobre nossa classe mais detalhadamente.";
			next;
			mes "[Mestre Miller]";
			mes "Como um Justiceiro, você deve";
			mes "manter sua arma com você";
			mes "o tempo todo. A Guilda dos";
			mes "Justiceiros mantém o controle de todas as Armas";
			mes "e Projéteis, então você só pode obtê-las";
			mes "dos membros da nossa guilda.";
			next;
			mes "[Mestre Miller]";
			mes "Não se preocupe, os membros";
			mes "da Guilda dos Justiceiros podem ser";
			mes "encontrados em quase todos os lugares, Enfim";
			mes "tem que ser desta maneira, por ordem da nossa líder da guilda, Lady Selena.";
			next;
			mes "[Mestre Miller]";
			mes "Você pode ter a chance de";
			mes "conhecê-la um dia desses.";
			mes "De qualquer forma, agora temos que";
			mes "controlar a venda de armas e projéteis para mantê-los longe de pessoas más";
			mes "ou irresponsáveis.";
			next;
			mes "[Mestre Miller]";
			mes "De qualquer forma, para mim,";
			mes "é sempre um prazer conversar";
			mes "com outro Justiceiro, então";
			mes "mantenha contato. Que o poder";
			mes "da Terra te proteja";
			mes "em todas as suas aventuras~";
			callfunc "Job_Change",Job_Gunslinger;
			set GUNS_Q,6;
			completequest 6024;
			if (rand(1,2) == 1) {
				getitem 13100,1;	// Six_Shooter
			}else{
				getitem 13150,1;	// Branch
			}
			close;
		}
	}else{
		if (Class == Job_Gunslinger) {
			mes "[Mestre Miller]";
			mes "Oh! A quanto tempo";
			mes "não te vejo, amigo. Como vai?";
			mes "Espero que você tenha mantido";
			mes "sua arma bem cuidada.";
			mes "Cuide dela, e ela cuidará";
			mes "de você. Lembre-se disso.";
			close;
		}else{
			mes "[Mestre Miller]";
			mes "Se você não tem";
			mes "nenhum assunto a tratar comigo,";
			mes "então, por favor, siga seu caminho.";
			close;
		}
	}
}

payon,184,65,3	script	Sábio Chifre de Touro	866,{
	if (GUNS_Q == 1) {
		mes "[Sábio Chifre de Touro]";
		mes "Olá, jovem lobo.";
		mes "Que assuntos te";
		mes "trazem à minha presença?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "O Sr. Miller me mandou";
		mes "entregar esta carta a você.";
		mes "Na verdade, estou interessado em";
		mes "me tornar um Justiceiro...";
		next;
		mes "[Sábio Chifre de Touro]";
		mes "Miller, você disse?";
		mes "Hm, o Raposa Negra não";
		mes "costuma escrever apresentações para qualquer um";
		mes "que ele não acredita que será";
		mes "um bom Justiceiro. Sim, eu acho que";
		mes "sei por que ele o mandou a mim.";
		next;
		mes "[Sábio Chifre de Touro]";
		mes "Posso ver em seus olhos que";
		mes "tem um bom coração e um";
		mes "forte senso de responsabilidade.";
		mes "Tudo o que precisa é a bênção";
		mes "da Terra para te proteger";
		mes "como Justiceiro.";
		next;
		mes "[Sábio Chifre de Touro]";
		mes "Eu posso fazer um amuleto que";
		mes "demonstrará seu desejo";
		mes "em se tornar um guerreiro da Terra,";
		mes "para que o apresente ao";
		mes "Mestre dos Justiceiros.";
		mes "Vou precisar destes itens...";
		next;
		mes "[Sábio Chifre de Touro]";
		mes "^3355FF1 Tronco^000000,";
		mes "^3355FF3 Felpas^000000,";
		mes "^3355FF3 Zargônios^000000,";
		mes "^3355FF10 Cascas^000000,";
		mes "^3355FF3 Ervas Verdes^000000, e";
		mes "^3355FF3 Cascos Arco-Íris^000000.";
		next;
		mes "[Sábio Chifre de Touro]";
		mes "Depois que eu terminar o";
		mes "amuleto, você deve levá-lo";
		mes "ao Raposa Negra, e ele irá";
		mes "o ajudar a alcançar";
		mes "seu objetivo de se tornar";
		mes "um Justiceiro.";
		set GUNS_Q,2;
		changequest 6020,6021;
		close;
	} else if(GUNS_Q == 2) {
		if (countitem(912) < 3 || countitem(914) < 3 || countitem(1019) < 1 || countitem(935) < 10 || countitem(511) < 3 || countitem(1013) < 3) {
			mes "[Sábio Chifre de Touro]";
			mes "Eu posso fazer um amuleto que";
			mes "demonstrará seu desejo";
			mes "em se tornar um guerreiro da Terra";
			mes "para que o apresente ao";
			mes "Mestre dos Justiceiros.";
			mes "Vou precisar destes itens...";
			next;
			mes "[Sábio Chifre de Touro]";
			mes "^3355FF1 Troncos^000000,";
			mes "^3355FF3 Felpas^000000,";
			mes "^3355FF3 Zargônios^000000,";
			mes "^3355FF10 Cascas^000000,";
			mes "^3355FF3 Ervas Verdes^000000, e";
			mes "^3355FF3 Cascos Arco-Íris^000000.";
			next;
			mes "[Sábio Chifre de Touro]";
			mes "Depois que eu terminar o";
			mes "amuleto, você deve levá-lo";
			mes "ao Raposa Negra, e ele irá";
			mes "o ajudar a alcançar";
			mes "seu objetivo de se tornar";
			mes "um Justiceiro.";
			close;
		}
		delitem 912,3;	// Zargon
		delitem 914,3;	// Fluff
		delitem 1019,1;	// Wooden_Block
		delitem 935,10;	// Shell
		delitem 511,3;	// Green_Herb
		delitem 1013,3;	// Colorful_Shell
		set GUNS_Q,3;
		changequest 6021,6022;
		mes "[Sábio Chifre de Touro]";
		mes "Ah, você voltou";
		mes "com tudo que eu preciso.";
		mes "Por favor, me dê algum tempo";
		mes "para fazer o amuleto. Se você";
		mes "voltar daqui a pouco,";
		mes "Eu devo ter terminado.";
		close;
	}
	else if(GUNS_Q == 3) {
		mes "[Sábio Chifre de Touro]";
		mes "Oh, você chegou na hora";
		mes "Já faz um tempo que";
		mes "eu fiz um desses amuletos, então devo estar um pouco";
		mes "enferrujado. Ainda assim, isso realmente me leva de volta para os dias de minha juventude.";
		next;
		mes "[Sábio Chifre de Touro]";
		mes "Eu estive servindo nesta";
		mes "posição de escolher pessoas";
		mes "dignas do amuleto do Justiceiro";
		mes "por algumas décadas";
		mes "Mas antes disso, eu era um";
		mes "jovem aventureiro, como você.";
		next;
		mes "[Sábio Chifre de Touro]";
		mes "Parece que foi ontem";
		mes "que eu ganhei meu próprio";
		mes "amuleto do Justiceri,";
		mes "um guerreiro da Terra. Foi quando";
		mes "eu conheci o pai de Selena.";
		mes "Como o tempo pode passar tão rápido?";
		next;
		mes "[Sábio Chifre de Touro]";
		mes "Ah... Eu realmente aprecio";
		mes "toda a ajuda de Selena e do";
		mes "Raposa Negra em ajuda em recrutar";
		mes "jovens Justiceiros. Eu estou bastante";
		mes "velho agora, e não consigo fazer tudo sozinho. *Suspiro...* Assim é a vida.";
		next;
		mes "[Sábio Chifre de Touro]";
		mes "Antes de você ir, posso";
		mes "pedir um pequeno favor?";
		mes "Estou com sede, e eu gostaria";
		mes "de um copo de leite gelado. Você";
		mes "poderia me trazer algum?";
		set GUNS_Q,4;
		changequest 6022,6023;
		close;
	} else if(GUNS_Q == 4) {
		if (countitem(519) < 1) {
			mes "[Sábio Chifre de Touro]";
			mes "Eu sou um velho homem que,";
			mes "em breve, se reunirá com a";
			mes "Mãe Terra. Você faria um favor";
			mes "a este velho Justiceiro de me";
			mes "trazer um copo de ^3131FFleite^000000 gelado, por favor?";
			close;
		}
		delitem 519,1;	// Milk
		set GUNS_Q,5;
		changequest 6023,6024;
		mes "[Sábio Chifre de Touro]";
		mes "Oh, obrigado por sua";
		mes "generosidade! Eu vejo que";
		mes "você me trouxe um pouco de";
		mes "leite. Ahhhh, delícia...";
		next;
		mes "[Sábio Chifre de Touro]";
		mes "Eu admiro a paciência,";
		mes "gentileza, e bondade";
		mes "que você provou trazendo";
		mes "isto para mim. Sim, essas são características que todos nós queremos ter.";
		next;
		mes "[Sábio Chifre de Touro]";
		mes "Agora, por favor, leve esse amuleto";
		mes "para Miller, o Raposa Negra, com";
		mes "toda minha aprovação.";
		mes "Eu espero que você use sua";
		mes "arma para apoiar a justiça como um";
		mes "nobre guerreiro da Terra.";
		next;
		mes "[Sábio Chifre de Touro]";
		mes "Eeh~Yeah~Eeh~Hooom";
		mes "Eeh~Yeah~Eeh~Hooom";
		mes "Maaaaarrraaa Neeey~";
		mes "Yippee Yippee Yai Yocaiyay~";
		close;
	} else if(GUNS_Q == 5) {
		mes "[Sábio Chifre de Touro]";
		mes "Por favor, leve esse comprovate";
		mes "para Miller, o Raposa Negra, com";
		mes "toda a minha aprovação.";
		mes "Eu espero que você use sua";
		mes "arma para apoiar a justiça como um";
		mes "nobre guerreiro da Terra.";
		next;
		mes "[Sábio Chifre de Touro]";
		mes "Eeh~Yeah~Eeh~Hooom";
		mes "Eeh~Yeah~Eeh~Hooom";
		mes "Maaaaarrraaa Neeey~";
		mes "Yippee Yippee Yai Yocaiyay~";
		close;
	} else if(GUNS_Q == 6) {
		mes "[Sábio Chifre de Touro]";
		mes "Faz muito tempo que não te vejo.";
		mes "Espero que tenha se tornado";
		mes "uma fera inteligente, que usa";
		mes "seus poderes como Justiceiro para protecer o que é bom e justo.";
		close;
	} else {
		mes "[Sábio Chifre de Touro]";
		mes "Zzzzzz~";
		mes "^333333*Uff*^000000";
		close;
	}
}